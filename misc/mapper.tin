#class mapper open;

#var flag_mapper_dbg 1;

#alias dbg
{
	#if {$flag_mapper_dbg == 1}
	{
		#format {logstr}{%t $id %0}{%m-%d %T};
		#line {log}{/ttplugin/log/robot}{$logstr};		
	}{
		#nop;
	};
};

#alias logmsg
{
	#format {logstr}{%t $id %1}{%m-%d %T};
	#line {log}{/ttplugin/log/robot}{$logstr};

	#if { "%2" == "true"}
	{
		#showme <129>===>%1<===<088>;
	};
};

#alias {mapper.get_area} {

	#class mapper.get_area.inner open;

	#var tmp_area_data[name] {};

	#if {"%1" == ""}
	{
		#alias tmp_callback #nop;
	}{
		#alias tmp_callback {%1};
	};

	#alias {get_area_done}
	{
		#class mapper.get_area.inner kill;
	};

	#action {这里暂时没有地图。} {
		tmp_callback;
		get_area_done;
	};
	#action {【%*地图】} {
		#var tmp_area_data[name] %%1;
		tmp_callback;
		get_area_done;
	};
	#action {【%*地图－%*】} {
		#var tmp_area_data[name] %%1%%2;
		tmp_callback;
		get_area_done;
	};

	#class mapper.get_area.inner close;

	localmaps;
	q;
};

#alias {mapper.get_room}
{
	#class mapper.get_room.inner open;

	#var tmp_room_data {};
	#var tmp_room_data[flag_room_data_line] 0;
	
	#if {"%1" == ""}
	{
		#alias tmp_callback #nop;
	}{
		#alias tmp_callback {%1};
	};

	#alias {tmp_work_done}
	{
		tmp_callback;

		#class mapper.get_room.inner  kill;
		#unact {^>%s};
	};
	#act {^%S%s-%s$}
	{
		#var tmp_room_data[name] %%1;
		#var tmp_room_data[desc] {};
		#var tmp_room_data[exit] {};
		#var tmp_room_data[npc] {};
		#var tmp_room_data[season] {};
		#var tmp_room_data[weather] {};
		#var tmp_room_data[is_indoor] 1;

		logmsg 开始扫描房间数据:${tmp_room_data[name]};
		#var tmp_room_data[flag_desc_line] 1;
		#var tmp_room_data[flag_room_data_line] 1;

		#act {^>%s}{tmp_work_done;};
	};

	#event {RECEIVED LINE}
	{
		#var tmp_room_data[rcvd_line] {%%1};

		#if {$tmp_room_data[flag_room_data_line] == 1 && "$tmp_room_data[rcvd_line]" != ""}
		{
			#nop dbg RCV_LINE==>$tmp_room_data[rcvd_line];

			#regexp {$tmp_room_data[rcvd_line]}{^%s这里%*出口是%*。$}
			{
				#var tmp_room_data[flag_desc_line] 0;
				#var tmp_room_data[exit] &3;

				#replace {tmp_room_data[exit]}{ }{};
				#replace {tmp_room_data[exit]}{和}{\;};
				#replace {tmp_room_data[exit]}{、}{\;};
			};

			#regexp {$tmp_room_data[rcvd_line]}{^%s你可以看看(look):%*$}
			{
				#var tmp_room_data[flag_desc_line] 0;
				#var tmp_room_data[lookable_item] {&2};

				#replace {tmp_room_data[lookable_item]}{,}{\;};
			};

			#regexp {$tmp_room_data[rcvd_line]}{^%s「%*」: %*。$}
			{
				#var tmp_room_data[flag_desc_line] 0;
				#var tmp_room_data[is_indoor] 0;
				#var tmp_room_data[season] &2;
				#var tmp_room_data[weather] &3;
			};

			#regexp {$tmp_room_data[rcvd_line]}{^%s%S「%S」%S(%w %w)%*}
			{
				#var tmp_room_data[npc][&5 &6] &4;
			}{
				#regexp {$tmp_room_data[rcvd_line]}{^%s%S(%w %w)%*}
				{
					#var tmp_room_data[npc][&3 &4] &2;
				}{
					#regexp {$tmp_room_data[rcvd_line]}{^%s%S%s%S(%w %w)%*}
					{
						#var tmp_room_data[npc][&5 &6] &4;
					}{
						#regexp {$tmp_room_data[rcvd_line]}{^%s%S%s%S%s%S(%w %w)%*}
						{
							#var tmp_room_data[npc][&7 &8] &6;
						};
					};
				};
			};

			#regexp {$tmp_room_data[rcvd_line]}{">%*"}
			{
				#var tmp_room_data[flag_room_data_line] 0;
				tmp_work_done;
			};

			#if {$tmp_room_data[flag_desc_line] == 1}
			{
				#var tmp_room_data[desc] {$tmp_room_data[desc]$tmp_room_data[rcvd_line]};
			};
		};
	};

	#class mapper.get_room.inner close;

	look;
};

#class mapper close;
