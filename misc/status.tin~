#class status open;

#VAR status[hpbrief_line_no] 1;
#VAR status[action] 空闲;
#var status[flag][bar] 1;

#list color_table[hp]		create 	{<ADA>}{<ADA>}{<BDA>}{<CDA>}{<DDA>}{<EDA>}{<FDA>}{<FCA>}{<FAA>}{<FAA>};
#list color_table[sp]		create 	{<AEC>}{<ADF>}{<BDF>}{<CDF>}{<DDF>}{<EDF>}{<FDF>}{<FCF>}{<FCF>}{<FBF>};
#list color_table[force]	create 	{<AAE>}{<ABE>}{<BBE>}{<CBE>}{<DBE>}{<EBE>}{<FBE>}{<FAE>}{<FAE>}{<FAC>};
#list color_table[energy]	create	{<BFF>}{<AEE>}{<BEE>}{<CEE>}{<DEE>}{<EEE>}{<FEE>}{<FDE>}{<FDE>}{<FCE>};
#list color_table[food]		create	{<AFC>}{<AEC>}{<BEC>}{<CEC>}{<DEC>}{<EEC>}{<FEC>}{<FDC>}{<FDC>}{<FCC>};
#list color_table[water]	create	{<AFC>}{<AEC>}{<BEC>}{<CEC>}{<DEC>}{<EEC>}{<FEC>}{<FDC>}{<FDC>}{<FCC>};

#function {fn_get_status_color}
{
	#var status_tmp @eval{%1*100/%2};

	#if {$status_tmp > 100}{#var color_code $color_table[%4][1]};
	#elseif {$status_tmp > 90}{#var color_code $color_table[%4][2]};
	#elseif {$status_tmp > 80}{#var color_code $color_table[%4][3]};
	#elseif {$status_tmp > 70}{#var color_code $color_table[%4][4]};
	#elseif {$status_tmp > 60}{#var color_code $color_table[%4][5]};
	#elseif {$status_tmp > 50}{#var color_code $color_table[%4][6]};
	#elseif {$status_tmp > 40}{#var color_code $color_table[%4][7]};
	#elseif {$status_tmp > 30}{#var color_code $color_table[%4][8]};
	#elseif {$status_tmp > 20}{#var color_code $color_table[%4][9]};
	#else {#var color_code $color_table[%4][10]};

	#if {"%3" == "f"}
	{
		#format color_code {%l}{$color_code};
	};
	
	#var result $color_code;

	#unvar color_code;
	#unvar status_tmp;
}

#function {fn_draw_status_bar}
{
	#var bar[size] 30;
	#var bar[code] @fn_get_status_color{%1;%2;b;%4};

	#var bar[width] @eval{%1*$bar[size]/%2};
	#if {$bar[width] == 0}
	{
		#if {%1 > 0}{#var bar[width] 1};
	};
	#var bar[valid_width] @eval{%3*$bar[size]/%2};

	#loop 0 @eval{$bar[size]-1} cnt
	{
		#if {$cnt == $bar[width]}{#var bar[code] {$bar[code]<EEE>}};
		#if {$cnt == $bar[valid_width]}{#var bar[code] {$bar[code]<CCC>}};
		#var bar[code] {$bar[code] };
	};
	#var bar[code] {$bar[code]<088>};

	#var result $bar[code];

	#nop #unvar bar;
	#nop #unvar cnt;
}

#alias {show_status_bar}
{
	#if {$status[flag][bar] == 0}{#return;};

	#math status[valid_hp_pct] ($status[hp_valid] * 100 / $status[hp_max]);
	#math status[valid_sp_pct] ($status[sp_valid] * 100 / $status[sp_max]);

	#format status[bar_hp] {%+4s%-30s %-5s/%+5s [%+3s\x25]}
		{气血}{@fn_draw_status_bar{$status[hp];$status[hp_max];$status[hp_valid];hp}}{$status[hp]}{$status[hp_valid]}{$status[valid_hp_pct]};

	#format status[bar_sp] {%+4s%-30s %-5s/%+5s [%+3s\x25]}
		{精神}{@fn_draw_status_bar{$status[sp];$status[sp_max];$status[sp_valid];sp}}{$status[sp]}{$status[sp_valid]}{$status[valid_sp_pct]};

	#format status[bar_force] {%+4s%-30s %-5s/%+5s}
		{内力}{@fn_draw_status_bar{$status[force];$status[force_max];$status[force_max];force}}{$status[force]}{$status[force_max]};

	#format status[bar_energy] {%+4s%-30s %-5s/%+5s}
		{精力}{@fn_draw_status_bar{$status[energy];$status[energy_max];$status[energy_max];energy}}{$status[energy]}{$status[energy_max]};

	#nop #format status[bar_food] {%+4s%-30s %-5s/%+5s}
		{食物}{@fn_draw_status_bar{$status[food];$status[food_max];$status[food_max];food}}{$status[food]}{$status[food_max]};

	#nop #format status[bar_water] {%+4s%-30s %-5s/%+5s}
		{饮水}{@fn_draw_status_bar{$status[water];$status[water_max];$status[water_max];water}}{$status[water]}{$status[water_max]};

	#format status[str_food] {%+6s%c%-4s/%+4s [%+4s]%c}
		{食物：}{@fn_get_status_color{$status[food];$status[food_max];b;food}}{$status[food]}{$status[food_max]}{$status[food_desc]}{<078>};

	#format status[str_water] {%+6s%c%-4s/%+4s [%+4s]%c}
		{饮水：}{@fn_get_status_color{$status[water];$status[water_max];b;water}}{$status[water]}{$status[water_max]}{$status[water_desc]}{<078>};

	#format status[str_overall]	{%+6s%-16s}{状态：}{$status[overall]};
	#format status[str_pot]		{%+6s%-8s}{潜能：}{$status[pot]};
	#format status[str_exp]		{%+6s%-8s}{经验：}{$status[exp]};
	#format status[str_level]	{%+6s%-7s}{等级：}{$status[level]};
	#format status[str_id]		{%+4s%-6s}{ID：}{$status[id]};
	#format status[str_name]	{%+6s%-8s}{姓名：}{$status[name]};
	#format status[str_age]		{%+6s%-5s}{年龄：}{$status[age]岁};
	#format status[str_ability]	{%+6s%-5s}{功力：}{$status[ability]年};
	#format status[str_moral]	{%+6s%-8s}{道德：}{$status[moral]};
	#format status[str_weight]	{%+6s%-4s}{负重：}{$status[weight_pct]};
	#format status[str_guild]	{%+6s%-8s}{门派：}{$status[guild]};
	#format status[str_loyal]	{%+6s%-5s}{门忠：}{$status[loyal]};
	#format status[str_reputation] {%+6s%-6s}{声望：}{$status[reputation]};
	#format status[str_money]	{%+6s%-8s}{存款：}{$status[money]};

	#echo {{%-53s%-6s%-8s%-6s%+46s}{2}}{<078>$status[bar_sp]}{     |}{<129>当前活动<078>}{|     }{$status[bar_energy]};
	#echo {{%-53s%-6s%-8s%-6s%+46s}{1}}{$status[bar_hp]}{     |}{<139>$status[action]<078>}{|     }{$status[bar_force]};
	
	#echo {{<EEE><aaa>%-120s<078>}{-2}}
		{$status[str_food]<EEE><aaa> $status[str_water]<EEE><aaa>  $status[str_pot] $status[str_exp] $status[str_level] $status[str_overall]};
	#echo {{<DDD><aaa>%-120s<078>}{-1}}
		{$status[str_id] $status[str_name] $status[str_age] $status[str_ability] $status[str_guild] $status[str_loyal] $status[str_moral] $status[str_reputation] $status[str_weight]};
}

#ACTION {^#%1,%2,%3,%4,%5,%6$}
{
	#if {$status[hpbrief_line_no] == 1}
	{
		#var tmp_exp %1;
		#var tmp_pot %2;
		#var tmp_force_max %3;
		#var tmp_force %4;
		#var tmp_energy_max %5;
		#var tmp_energy %6;

		#var status[hpbrief_line_no] 2;
	};
	#elseif {$status[hpbrief_line_no] == 2}
	{
		#var status[hp_max] %1;
		#var status[hp_valid] %2;
		#var status[hp] %3;
		#var status[sp_max] %4;
		#var status[sp_valid] %5;
		#var status[sp] %6;

		#var status[exp] $tmp_exp;
		#var status[pot] $tmp_pot;
		#var status[force_max] $tmp_force_max;
		#var status[force] $tmp_force;
		#var status[energy_max] $tmp_energy_max;
		#var status[energy] $tmp_energy;

		show_status_bar;
	};
	
	#line {gag};
}
{5}

#ACTION {^【 食物 】%s%1%s/%s%2%s[%3]%*}
{
	#var status[food] %1;
	#var status[food_max] %2;
	#var status[food_desc] %3;
}
{5}

#ACTION {^【 饮水 】%s%1%s/%s%2%s[%3]%*}
{
	#var status[water] %1;
	#var status[water_max] %2;
	#var status[water_desc] %3;
}
{5}

#ACTION {^【 状态 】%s%1}
{
	#var status[overall] %1;
	show_status_bar;
}
{5}

#act {^≡━◎个人档案◎━━━━━━━━━━━━━━━━━━━━━━━━≡$}
{
	#class tmp open;
	#act {^【%s%S%s%S%s】%S{ |」}%S(%w)$}{#var status[name] %%8;#var status[id] %%9;};
	#act {^%s你是一位%%1岁的%*婚%%2性人类%*}{#var status[age] @ctd{%%1};#var status[gender] %%2;};
	#act {^%s银行存款：%s%S$}{#var status[money] %%3;};
	#act {^%s实战经验：%s%S年功力%*}{#var status[ability] @ctd{%%3};};
	#act {^%s等%s级：%s%S$}{#var status[level] %%4;};
	#act {^%s道%s德：%s%S%s潜%s能%*}{#var status[moral] %%4;};
	#act {^%s师门忠诚：%s%S%s江湖声望：%s%S$}{#var status[loyal] %%3;#var status[reputation] %%6;};
	#act {你是%*第%*代弟子} {#var status[guild] %%1};
	#act {你是朝廷%*品官员} {#var status[guild] 朝廷};
	#act {≡━━━━━━━━━━━━━━━━━━━━━━◎北大侠客行◎━━≡} {#class tmp kill};
	#class tmp close;
};

#act {^│%s你身上带着%*件东西%s(负重%s%S)%*}{#var status[weight_cnt] %2;#var status[weight_pct] %5;}

#act {^#SESSION%*ACTIVATED%*}{hpbrief};
#act {%*你%*请教%*疑问%*}{hpbrief};
#act {%*你%*练习%*}{hpbrief};
#act {^你突然感到精神一振，浑身似乎又充满了力量！} {hpbrief};
#act {^( 你似乎有些疲惫，但是仍然十分有活力。 )} {hpbrief};
#act {^( 你受了几处伤，不过似乎并不碍事。 )} {hpbrief};
#act {^( 你动作似乎开始有点不太灵光，但是仍然有条不紊。 )} {hpbrief};
#act {^( 你%*，看起来状况并不太好。 )} {hpbrief};
#act {^( 你已经%*，正在勉力支撑著不倒下去。 )} {hpbrief};
#act {^( 你看起来已经力不从心了。 )} {hpbrief};
#act {^( 你气息粗重，动作开始散乱，看来所受的伤著实不轻。 )} {hpbrief};
#act {^( 你似乎十分疲惫，看来需要好好休息了。 )} {hpbrief};
#act {^( 你受了相当重的伤，只怕会有生命危险。 )} {hpbrief};
#act {^( 你%*，眼看就要倒在地上。 )} {hpbrief};
#act {^( 你已经陷入半昏迷状态，随时都可能摔倒晕去。 )} {hpbrief};
#act {^( 你受伤过重，已经有如风中残烛，随时都可能断气。 )} {hpbrief};
#act {^你深深吸了几口气，脸色看起来好多了。} {hpbrief};
#act {^你领悟出一些基本轻功方面的窍门。} {hpbrief};
#act {^你身子发虚}{hpbrief};

#ALIAS {hpbrief}
{
	#var status[hpbrief_line_no] 1;
	#send hpbrief;
}
{4};

#alias {statusbar}
{
	#if {$status[flag][bar] == 1}
	{
		#var status[flag][bar] 0;
		#split;
	}{
		#var status[flag][bar] 1;
		#split 2 2;
		hpbrief;
	};
};

#TICKER {status_refresh}{hpbrief}{15};
#TICKER {status_refresh2}{i;hp}{120};
#TICKER {status_refresh3}{sc}{300};

#split 2 2;
hpbrief;sc;i;hp;

#class status close;
