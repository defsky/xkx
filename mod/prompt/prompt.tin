#class prompt open;

#function {__prompt_init__}{
    #var prompt_config {{status}{off}};
    
    prompt.Init;
    prompt.Enable;

    #return true;
};
#function {__prompt_reload__}{
    prompt.Init;
    prompt.Enable;
    #return true;
};
#function {__prompt_unload__}{
    prompt.Disable;
    #return true;
};

#nop prompt æ˜¯ä¸€ä¸ªæç¤ºæ æ’ä»¶ï¼Œæ”¯æŒä¸°å¯Œçš„è‡ªå®šä¹‰é€‰é¡¹;

/*
æ•´ä¸ªå±å¹•åˆ’åˆ†ä¸ºä¸ƒä¸ªåŒºåŸŸï¼Œä»ä¸Šåˆ°ä¸‹ä¾æ¬¡ä¸ºï¼š
Top line1
Top line2
...
Top lineN
----------- TopSepBar --------------
ï¼ˆæ¸¸æˆåŒºï¼‰
----------- MidSepBar --------------
Bot line1
Bot line2
...
Bot lineN
----------- BotSepBar --------------
ï¼ˆè¾“å…¥åŒºï¼‰

é™¤äº†è¾“å…¥åŒºå’Œæ¸¸æˆåŒºä¹‹å¤–ï¼Œå‰©ä¸‹äº”ä¸ªåŒºåŸŸéƒ½å¯ä»¥å®šåˆ¶ï¼Œç”¨æ¥æ˜¾ç¤ºä¿¡æ¯å­—æ®µã€‚

ä¿¡æ¯å­—æ®µçš„ç¼–æ’æ˜¯é«˜åº¦å¯å®šåˆ¶çš„ï¼Œæ¯ä¸ªå­—æ®µçš„æ ‡ç­¾ã€å†…å®¹ã€é¢œè‰²ã€æ˜¾ç¤ºé£æ ¼ã€è¡Œä¸ºå±æ€§ï¼Œ
éƒ½å¯ä»¥é€šè¿‡è‡ªå·±ä¿®æ”¹ #list prompt-fields æ¥ä¿®æ”¹ã€‚

ä¸‹é¢æ˜¯å¯¹ #list prompt-fields
çš„æ ¼å¼è¯´æ˜ï¼Œä½†ä¸å»ºè®®ç›´æ¥åœ¨æ­¤å¤„ä¿®æ”¹å˜é‡ï¼Œè¯·é€šè¿‡ etc/ui-settings.tin æ¥ä¿®æ”¹ã€‚
prompt æ¨¡å—åœ¨å¯åŠ¨æ—¶ä¼šè‡ªåŠ¨åŠ è½½è¯¥æ–‡ä»¶ã€‚

example:
#list prompt-fields create {
    { {place}{BotSepBar}  {label}{URL}  {name}{URL}  {cooldown}{180} }
};

#list prompt-fields æ˜¯ä¸€ä¸ªåˆ—è¡¨ï¼Œå…¶ä¸­ä¸­æ¯ä¸ªå…ƒç´ æ˜¯ä¸€ä¸ª tableï¼Œä»£è¡¨ä¸€ä¸ªä¿¡æ¯ã€‚


table çš„å¯ç”¨å­—æ®µæœ‰ï¼š
1. place æšä¸¾å€¼ å¯é€‰é¡¹: {Top|TopSepBar|MidSepBar|Bot|BotSepBar}
  å¸Œæœ›å°†ä¿¡æ¯æ˜¾ç¤ºåœ¨å±å¹•ä¸Šçš„ä»€ä¹ˆä½ç½®
  å…¶ä¸­ {Top} å’Œ {Bot} æ”¯æŒå¤šè¡Œï¼Œå…¶å®ƒä¸‰ä¸ªåªæœ‰ä¸€è¡Œ
  é»˜è®¤ä¸º BotSepBar

2. line è‡ªç„¶æ•°
   åªæœ‰ place={Top|Bot} æ‰æ”¯æŒ lineï¼Œä»£è¡¨ç¬¬å‡ è¡Œï¼Œæœ€ä¸Šé¢æ˜¯ç¬¬ä¸€è¡Œ
   é»˜è®¤ä¸º 1

3. order è‡ªç„¶æ•°
   åœ¨åŒä¸€è¡Œä¸­çš„æ¬¡åºï¼Œé»˜è®¤æŒ‰ç…§åœ¨ list ä¸­å‡ºç°çš„é¡ºåºæ¥æ’åºã€‚

4. label ä¸­è‹±æ–‡å­—ç¬¦ä¸²
   ä¿¡æ¯æ ‡ç­¾ï¼Œæ¯ä¸ªä¿¡æ¯éƒ½å¯ä»¥æœ‰ä¸€ä¸ªæ ‡ç­¾ï¼Œæ˜¾ç¤ºåœ¨å†…å®¹å‰é¢ã€‚
   é»˜è®¤ä¸ºç©ºç™½

5. color tintin æ ¼å¼çš„é¢œè‰²ä»£ç ï¼Œä¾‹å¦‚ <ddd>
   ä¿¡æ¯æ ‡ç­¾çš„é¢œè‰²ï¼Œé€šè¿‡è¯¥é€‰é¡¹å¯ä»¥ä¸ºæ ‡ç­¾æŒ‡å®šä¸€ä¸ªä¸é…è‰²ä¸»é¢˜ä¸ä¸€æ ·çš„é¢œè‰²ã€‚
   ä¸€èˆ¬æ¥è¯´åªæœ‰æä¸ªåˆ«é€‰é¡¹æ‰éœ€è¦ç‰¹åˆ«çš„é¢œè‰²ï¼Œæ²¡å¿…è¦ä¸ºæ¯ä¸ªé€‰é¡¹éƒ½æŒ‡å®šé¢œè‰²ï¼Œ
   å› ä¸ºé‚£æ ·çš„è¯ä½ è¿˜ä¸å¦‚å»è®¾ç½®é…è‰²ä¸»é¢˜ï¼Œå‚è§ä¸‹é¢é…è‰²ä¸»é¢˜çš„è®¾ç½®ã€‚
   é»˜è®¤ä¸ºç©ºç™½ï¼Œä»£è¡¨é‡‡ç”¨é…è‰²ä¸»é¢˜.

6. name è‹±æ–‡å­—ç¬¦ä¸² ä¸èƒ½ä¸ºç©º
   ä¿¡æ¯åç§°ï¼Œå¿…é¡»å”¯ä¸€ï¼Œä¸èƒ½é‡å¤ã€‚

7. width æ•´æ•°
   ä¿¡æ¯å®½åº¦ï¼Œå¦‚æœä¿¡æ¯å†…å®¹é•¿åº¦å˜åŒ–è¾ƒå¤§ï¼Œå¯ä»¥è®¾ç½®ä¸€ä¸ªå›ºå®šå®½åº¦ï¼Œä»¥å…æ™ƒåŠ¨ã€‚
   é»˜è®¤ä¸º 0ï¼Œè¡¨ç¤ºæŒ‰ç…§å†…å®¹é•¿åº¦è‡ªåŠ¨é€‚é…ã€‚

8. visibility æšä¸¾å€¼ {AutoHide|HideLabel|Always}
   å«ä¹‰å¦‚ä¸‹ï¼š
      * AutoHide å¦‚æœä¿¡æ¯å†…å®¹ä¸ºç©ºï¼Œåˆ™ä¸æ˜¾ç¤ºæ­¤é¡¹å†…å®¹
      * HideLabel ä»…æ˜¾ç¤ºå†…å®¹ï¼Œä¸æ˜¾ç¤ºæ ‡é¢˜
      * Always ä¸è®ºå†…å®¹æ˜¯å¦ä¸ºç©ºï¼Œå§‹ç»ˆæ˜¾ç¤ºã€‚
   é»˜è®¤ä¸º AutoHide

9. cooldown éè´Ÿæ•´æ•°
   è¯¥ä¿¡æ¯çš„æœ‰æ•ˆæ—¶é—´ã€‚å¦‚æœ cooldown ä¸ä¸º 0ï¼Œåˆ™è¯¥ä¿¡æ¯è¢«æ›´æ–°æ—¶å…¶æ ‡ç­¾å°†ä¼šä»¥
   é¢œè‰²ä¸»é¢˜ä¸­ HotLabel æ‰€æŒ‡å®šçš„é£æ ¼æ˜¾ç¤ºå¹¶æŒç»­è‹¥å¹²ç§’åï¼Œè½¬ä¸ºç”± CoolLabel
   æ‰€æŒ‡å®šçš„é£æ ¼æ˜¾ç¤ºã€‚æŒç»­æ—¶é—´ç”± cooldown å­—æ®µçš„å€¼æ¥å†³å®šï¼Œå•ä½ä¸ºç§’ã€‚
   å¦‚æœ cooldown ä¸º 0ï¼Œåˆ™å§‹ç»ˆä»¥ HotLabel æ‰€æŒ‡å®šçš„é£æ ¼æ˜¾ç¤ºã€‚
   æ³¨æ„ï¼Œcooldown ä¸ä¸ºé›¶çš„å­—æ®µå°†è‡ªåŠ¨è·å¾— visibility=AutoHide å±æ€§ï¼Œæ— è®º
   visibility é€‰é¡¹åŸæœ¬è¢«è®¾ç½®æˆäº†ä»€ä¹ˆã€‚
   é»˜è®¤å€¼ä¸º 0

10. countdown æšä¸¾å€¼ {Auto|Clock|Seconds}
   å€’è®¡æ—¶ç±»å‹çš„å­—æ®µï¼Œå…¶å†…å®¹ä¸­æ‰€æœ‰å½¢å¦‚ (%d) çš„å†…å®¹å°†ä¼šè¢«æ›¿æ¢æˆå€’è®¡æ—¶æ˜¾ç¤º,
   å…¶ä¸­()ç”¨æ¥éš”ç¦»å…¶å®ƒæ–‡æœ¬å†…å®¹ã€‚
   æœ¬æ’ä»¶ä¼šè‡ªåŠ¨ä¸ºè¯¥å­—æ®µæ›´æ–°å…¶å†…å®¹ï¼Œä»¥ç¡®ä¿ç”¨æˆ·èƒ½å¤Ÿçœ‹åˆ°å€’è®¡æ—¶çš„æ•ˆæœã€‚
   å€’è®¡æ—¶æœ‰ä¸¤ç§æ˜¾ç¤ºæ ·å¼ï¼Œæ—¶é’Ÿæ ·å¼ï¼ˆNå¤© HH:MM:SSï¼‰å’Œè¯»ç§’æ ·å¼ï¼ˆNç§’ï¼‰ã€‚å¦‚æœ
   è®¾ç½®ä¸º Auto åˆ™æ ¹æ®å‰©ä½™æ—¶é—´é•¿çŸ­ï¼Œè‡ªåŠ¨åˆ‡æ¢ä¸¤ç§æ˜¾ç¤ºæ•ˆæœã€‚

ä¸å»ºè®®ç›´æ¥åœ¨æ­¤å¤„ä¿®æ”¹å˜é‡ï¼Œè¯·é€šè¿‡ etc/ui-settings.tin æ¥ä¿®æ”¹ã€‚

example:
#list prompt-fields create {
    { {place}{BotSepBar}  {label}{URL}  {name}{URL}  {cooldown}{180} }
};

*/
#list prompt-fields create {};

#nop é…è‰²ä¸»é¢˜ï¼Œæ³¨æ„è¿™é‡Œä¸è¦ç›´æ¥åµŒå…¥ SGRï¼ˆansi codesï¼‰ï¼Œå¦åˆ™è®¡ç®—å®½åº¦æ—¶ä¼šæœ‰é—®é¢˜ã€‚;
#var prompt-theme {
    {Disable}{<100>}
    {TopSepBar}{<020>}
    {MidSepBar}{<020>}
    {BotSepBar}{<020>}
    {HotLabel}{<100>}
    {CoolLabel}{<070>}
    {Value}{<070>}
};

#nop è‡ªå®šä¹‰å›¾æ ‡;
#var prompt-icon {
    {DisableRefresh}{ğŸš«}
};

#nop çƒ­é”®ç»‘å®š;
#list global-key-bindings create {
    {{key}{\cs}    {action}{prompt.ToggleSwitch}}
};

#var prompt-top-max-line {0};
#var prompt-bot-max-line {0};

#nop æ‰€æœ‰çš„å­—æ®µå€¼çš„å­—å…¸;
#var prompt-dict {};

#nop ä¸Šæ¬¡å®é™…ç»˜åˆ¶å±å¹•çš„æ—¶é—´;
#var prompt-last-draw {0};

#alias {prompt.Init} {
    #read {etc/ui-settings.tin};
    prompt.Set {};
    prompt.bindKey;
};

#nop è®¾ç½®å­—æ®µå€¼ï¼Œè¢«è®¾ç½®çš„å­—æ®µå€¼å°†ç«‹å³æ˜¾ç¤ºåœ¨å±å¹•ä¸Š;
#alias {prompt.Set} {
    #local fields   {%1};

    #local now {};
    #format now {%T};

    #local field    {};
    #foreach {*fields[]} {field} {
        #var prompt-dict[$field] {
            {updateTime}{$now}
            {showTime}{$now}
            {value}{$fields[$field]}
        };
    };

    prompt.refresh;
};

#nop è®¾ç½®å­—æ®µå€¼ï¼Œè¢«è®¾ç½®çš„å­—æ®µå€¼å°†ç«‹å³æ˜¾ç¤ºåœ¨å±å¹•ä¸Šï¼Œä½†ä¼šåœ¨ä¸€æ®µæ—¶é—´åæ¶ˆå¤±;
#alias {prompt.SetAwhile} {
    #local id    {%1};
    #local value {%2};
    #local secs  {%3};

    #if { "$secs" == "" } {
        #local secs {5};
    };

    prompt.Set {{$id}{$value}};

    #line sub {VARIABLES} {
        #delay prompt-set-awhile-$id {
            prompt.Set {{$id}{}};
        } {$secs};
    };
};

#nop æ˜¾ç¤ºå­—æ®µå€¼;
#alias {prompt.refresh} {
    #local topMaxLine   {0};
    #local botMaxLine   {0};

    #local topLines     {};
    #local botLines     {};
    #local topSepBar    {};
    #local midSepBar    {};
    #local botSepBar    {};

    #foreach {*{prompt-fields[]}} {idx} {
        #local field {${prompt-fields[$idx]}};
        #if { "$field[visibility]" == "" } {
            #local field[visibility] {AutoHide};
        };
        #switch {"$field[place]"} {
            #case {"Top"} {
                #while {1} {
                    #if { $field[line] <= $topMaxLine } {
                        #break;
                    };
                    #math topMaxLine {$topMaxLine + 1};
                    #local topLines[$topMaxLine] {};
                };
                #if { "$field[order]" == "" } {
                    #local field[order] {@eval{ @max{{0;*topLines[$field[line]][]}} + 1 }};
                };
                #local topLines[$field[line]][$field[order]] {$field};
            };
            #case {"Bot"} {
                #while {1} {
                    #if { $field[line] <= $botMaxLine } {
                        #break;
                    };
                    #math botMaxLine {$botMaxLine + 1};
                    #local botLines[$botMaxLine] {};
                };
                #if { "$field[order]" == "" } {
                    #local field[order] {@eval{ @max{{0;*botLines[$field[line]][]}} + 1 }};
                };
                #local botLines[$field[line]][$field[order]] {$field};
            };
            #case {"TopSepBar"} {
                #if { "$field[order]" == "" } {
                    #local field[order] {@eval{ @max{{0;*topSepBar[]}} + 1 }};
                };
                #local topSepBar[$field[order]] {$field};
            };
            #case {"MidSepBar"} {
                #if { "$field[order]" == "" } {
                    #local field[order] {@eval{ @max{{0;*midSepBar[]}} + 1 }};
                };
                #local midSepBar[$field[order]] {$field};
            };
            #case {"BotSepBar"} {
                #if { "$field[order]" == "" } {
                    #local field[order] {@eval{ @max{{0;*botSepBar[]}} + 1 }};
                };
                #local botSepBar[$field[order]] {$field};
            };
            #default {
            };
        };
    };

    #local barColor {};
    #if { "${prompt-dict[switch][value]}" != "" } {
        #local barColor {${prompt-theme[Disable]}};
    };

    #if { "$topSepBar" != "" } {
        #math topMaxLine {$topMaxLine + 1};
    };

    #if { $botMaxLine == 0 && ( "$midSepBar" == "" || "$botSepBar" == "" ) } {
        #local botMaxLine {1};
    };
    #else {
        #math botMaxLine {$botMaxLine + 2};
    };

    #if {  "${prompt-top-max-line}" != "$topMaxLine"
        || "${prompt-bot-max-line}" != "$botMaxLine" } {
        #var prompt-top-max-line {$topMaxLine};
        #var prompt-bot-max-line {$botMaxLine};
        #local arg1 {$topMaxLine};
        #local arg2 {$botMaxLine};
        #split $arg1 $arg2;
    };

    #if { "$topSepBar" != "" } {
        #local text {@__prompt_build_line__{{$topSepBar}}};
        #if { "$barColor" == "" } {#local barColor {${prompt-theme[TopSepBar]}}};
        #local text {@__prompt_fill_line__{{$text[text]};{$text[width]};$barColor}};
        #echo {{%s}{$topMaxLine}} {$text};
    };

    #local line {};
    #foreach {*topLines[]} {line} {
        #local fields {$topLines[$line]};
        #local text {@__prompt_build_line__{{$fields}}};
        #echo {{%s}{$line}} {$text[text]};
    };

    #if { "$midSepBar" != "" } {
        #local text {@__prompt_build_line__{{$midSepBar}}};
        #if { "$barColor" == "" } {#local barColor {${prompt-theme[MidSepBar]}}};
        #local text {@__prompt_fill_line__{{$text[text]};{$text[width]};$barColor}};
        #math line {$botMaxLine + 1};
        #echo {{%s}{-$line}} {$text};
    };

    #foreach {*botLines[]} {line} {
        #local fields {$botLines[$line]};
        #local text {@__prompt_build_line__{{$fields}}};
        #math line {$botMaxLine - $line + 1};
        #echo {{%s}{-$line}} {$text[text]};
    };

    #if { "$botSepBar" != "" } {
        #local text {@__prompt_build_line__{{$botSepBar}}};
        #if { "$barColor" == "" } {#local barColor {${prompt-theme[BotSepBar]}}};
        #local text {@__prompt_fill_line__{{$text[text]};{$text[width]};$barColor}};
        #echo {{%s}{-2}} {$text};
    };
};

#function {__prompt_build_line__} {
    #local fields       {%1};
    #local text         {};
    #local order        {};
    #local lineWidth    {0};
    #foreach {*fields[]} {order} {
        #local field {$fields[$order]};
        #local name  {$field[name]};
        #local label {$field[label]};
        #local color {$field[color]};
        #local width {$field[width]};
        #local value {${prompt-dict[$name]}};
        #local now {};
        #format now {%T};

        #local cool {false};

        #if { "$color" == "" } {
            #if { $field[cooldown] > 0 && $now - $value[updateTime] > $field[cooldown] } {
                #local color {${prompt-theme[CoolLabel]}};
                #local cool {true};
            };
            #else {
                #local color {${prompt-theme[HotLabel]}};
            };
        };

        #if { "${prompt-dict[switch][value]}" != "" } {
            #local color {${prompt-theme[Disable]}};
        };

        #if { "$value[value]" == "" && ( "$field[visibility]" == "AutoHide" || "$cool" == "true" ) } {
            #continue;
        };

        #if { "$label" != "" } {
            #local label {$color$label<070> };
        };

        #if { "$field[visibility]" == "HideLabel" } {
            #local label {};
        };

        #if { "$label$value[value]" == "" } {
            #continue;
        };

        #local color {${prompt-theme[Value]}};

        #if { "$field[countdown]" != "" } {
            #local seconds {};
            #math seconds {$now - ${prompt-dict[$name][showTime]}};
            #replace value[value] {^%+1..d$} {@__prompt_countdown__{&1;$seconds}};
            #replace value[value] {(%+1..d)} {(@__prompt_countdown__{&1;$seconds})};
            #var {prompt-dict[$name][showTime]} {$now};
            #var {prompt-dict[$name][value]} {$value[value]};
            #replace value[value] {^%+1..d$} {@__prompt_show_countdown__{$field[countdown];&1}};
            #replace value[value] {(%+1..d)} {(@__prompt_show_countdown__{$field[countdown];&1})};
        };

        #format value {$color%-${width}s<070>} {$value[value]};
        #math lineWidth {$lineWidth + @strWidth{$label} + @strWidth{$value}};
        #if { "$text" == "" } {
            #local text {$label$value};
        };
        #else {
            #local text {$text $label$value};
            #math lineWidth {$lineWidth + 1};
        };
    };
    #return {{width}{$lineWidth}{text}{$text}};
};

#function {__prompt_countdown__} {
    #local value    {%1};
    #local count    {%2};

    #math value {$value - $count};
    #if { $value < 0 } {
        #local value {0};
    };

    #return {$value};
};

#function {__prompt_show_countdown__} {
    #local type     {%1};
    #local secs     {%2};
    #local ret      {};

    #if { "$type" == "Auto" } {
        #if { $secs > 600 } {
            #local type {Clock};
        };
        #else {
            #local type {Seconds};
        };
    };

    #if { "$type" == "Clock" } {
        #if { $secs > 86400 } {
            #math ret {$secs / 86400};
            #local ret {$retå¤©};
            #math secs {$secs % 86400};
        };

        #local hour {};
        #local mins {};
        #math hour {$secs / 3600};
        #math secs {$secs % 3600};
        #math mins {$secs / 60};
        #math secs {$secs % 60};
        #format ret {%s%%02d:%%02d:%%02d} {$ret} {$hour} {$mins} {$secs};
    };
    #elseif { "$type" == "Seconds" } {
        #local ret {${secs}s};
    };

    #return {$ret};
};

#function {__prompt_fill_line__} {
    #local text     {%1};
    #local width    {%2};
    #local color    {%3};

    #local newText {@trim{$text}};
    #math width {$width + @strWidth{$newText} - @strWidth{$text}};

    #local screenWidth {0};
    #screen get COLS screenWidth;

    #local leftLen {0};
    #local rightLen {0};
    #math leftLen {($screenWidth - $width) / 2 - 1};
    #math rightLen {$screenWidth - $leftLen - $width - 2};

    #local left {};
    #format {left} {%${leftLen}s} {};
    #replace {left} { } {â”€};

    #local right {};
    #format {right} {%${rightLen}s} {};
    #replace {right} { } {â”€};

    #if { $leftLen < 0 } {
        #format {newText} {%$screenWidth.${screenWidth}s} {$newText};
        #return {$newText};
    };
    #elseif { "$newText" == "" } {
        #return {$color$leftâ”€â”€$right<070>};
    };
    #else {
        #return {$color$left<070> $newText $color$right<070>};
    };
};

#event {SCREEN RESIZE} {
    #var prompt-top-max-line {0};
    #var prompt-bot-max-line {0};
    prompt.refresh;
};

#alias {prompt.updateHP} {
    prompt.Set {
        {exp}{$char[HP][ç»éªŒæ˜¾ç¤º]}
        {pot}{$char[HP][æ½œèƒ½æ˜¾ç¤º]}
    }
};

#alias {prompt.updateSM} {
    prompt.Set {
        {yunqi}{$char[STATUS][æ°”è¡€æ¢å¤]}
        {status}{$char[STATUS][å¥åº·çŠ¶æ€]}
        {persist}{$char[STATUS][æŒç»­æ•ˆæœ]}
    }
};

#alias {prompt.Disable} {
    prompt.Set {{switch}{${prompt-icon[DisableRefresh]}}};
    #untick prompt.refresh;
};

#alias {prompt.Enable} {
    prompt.Set {{switch}{}};
    #tick prompt.refresh {prompt.refresh} 1;
};

#alias {prompt.ToggleSwitch} {
    #if { "${prompt-dict[switch][value]}" == "" } {
        prompt.Disable;
    };
    #else {
        prompt.Enable;
    };
};

#alias {prompt.bindKey} {
    #local idx {};
    #foreach {*{global-key-bindings[]}} {idx} {
        #local key  {${global-key-bindings[$idx][key]}};
        #local code {${global-key-bindings[$idx][action]}};
        #line sub var #macro {$key} {$code};
    };
};

#alias {prompt.test} {
    #local fullme {<010>å¾ˆä¹…æ²¡æœ‰è¿›è¡Œæœºå™¨äººæ£€æŸ¥(fullme)äº†ï¼Œä»»åŠ¡å¥–åŠ±å°†å—åˆ°å½±å“ã€‚<070>};
    #local fullme {http://pkuxkx.com/antirobot/robot.php?filename=1576762922984895};
    prompt.Set { {URL}{$fullme} };
    prompt.Set {
        {pot}{357.75ä¸‡}
        {exp}{7510.56ä¸‡}
        {expSpd}{11.88ä¸‡/å°æ—¶}
        {profit}{308é‡‘å¸/å°æ—¶}
    };
    prompt.Set {
        {job}{<171>ä¹”å³°}
        {stage}{å¯»æ‰¾NPC}
        {area}{å»ºåº·}
        {room}{ä¸­åŸ}
        {npc}{åºä¹å…¬}
        {type}{æ€æ­»}
    };
};

#class prompt close;
