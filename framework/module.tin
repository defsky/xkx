#class frm_mod open;

#var modules {};

#alias {mod} {
    #var mod_name %1;
    #format {file} {module/%s.tin} {%1};
    #if {"%2" == "load"} {
        #script {is_file} {if [ -f $file ]; then echo 1;fi;};
        #if {$is_file == 1} {
            #if {$modules[%1] == 0} {
                #read $file;
                #var modules[%1] 1;
            } {
                dbg module %1 has been loaded;
            };
        } {
            dbg module file $file not found;
        };
        #unvar is_file;
    };
    #elseif {"%2" == "unload"} {
        #if {&modules[%1] != 0 && $modules[%1] == 1} {
            %1_unload;
            #var modules[%1] 0;
        } {
            dbg module %1 has not be loaded;
        };
    };
    #elseif {"%2" == "reload"} {
        mod %1 unload;
        mod %1 load;
    };
    #else {
        #if {"%1" == "list"} {
            _update_mods;
            #format {out_str}{%-14s%+6s}{Module Name}{Loaded};
            #showme -----------------------------;
            #showme $out_str;
            
            #foreach {$modules[]} {m} {
                #format {out_str}{%-14s%+6s}{$m}{$modules[$m]};
                #showme $out_str;
            };
            
            #unvar out_str;#unvar m;
            #buffer lock;
        };
        #else {
            #showme <129> Usage : mod [<list | mod name <load | unload | reload>>]
        };
    };

    #unvar file;#unvar mod_name;
}

#alias {startup_mod_init} {
    #foreach {$startup_modules[%*]} {m} {
        mod $m load;
    };
    #unvar m;#unvar startup_modules;
};

#alias {_update_mods} {
    #script {mods}{ls module/*.tin | cut -d'/' -f2 | cut -d'.' -f1};
    #foreach {$mods[%*]}{m} {
        #if {&modules[$m] == 0} {
            #var modules[$m] 0;
        };
    };
    #unvar mods;
};
#class frm_mod close;

_update_mods;

#unvar m;#unvar mods;
