#class $mod_name open;

#alias {${mod_name}_load} {
    _fskmsg 依赖map模块,请手动加载;
    #showme Module $mod_name loaded;
    #unalias ${mod_name}_load;
};

#alias {${mod_name}_unload}{
    fsk stop;
    #showme Module $mod_name unloaded;
    #class $mod_name kill;
};

#alias {${mod_name}_help}{
    #showme <139>模块帮助文档 : ${mod_name}<079>;
    
    #showme <139> 描述 : 领悟、练习指定skills到上限<079>;
    
};

#var jiudai_empty 1;

#act {^牛皮酒袋已经被喝得一滴也不剩了} {
    #if {$jiudai_empty == 1} {
        #var jiudai_empty 0;
        put jiudai in bao 2;
        get jiudai from bao;
        he jiudai;
        #delay {5} {#var jiudai_empty 1;};
    };
};

#alias {_fskmsg} {
    #showme <139>fullskill : %0<079>;
};

#alias {fsklimit} {
    #var fsk[config][limit] %1;
};
#alias {fskfreq} {
    #if {"%1" == "lw"} {
        #var fsk[config][freq][lw] %2;
    };
    #elseif {"%1" == "lx"} {
        #var fsk[config][freq][lx] %2;
    };
    #else {
        #var fsk[config][freq][lw] %1;
        #var fsk[config][freq][lx] %1;
    };
};
#alias {fskpath} {
    #var fsk[config][path] %0;
    #path load $fsk[config][path];
    #path save backward fsk[config][rpath];
};
#alias {fskadd} {
    #var tmp_sk[base] %1;
    #var tmp_sk[special] %2;
    #list fsk[skills] add {{$tmp_sk}};
    #list fsk[skills] size fsk[config][skill_count];
    #unvar tmp_sk;
};
#alias {fskdel} {
    #list fsk[skills] delete %1;
    #list fsk[skills] size fsk[config][skill_count];
};

#alias on_next_skill {
    #alias cb_on_next_skill #cr;
    #alias cb_on_next_skill %0;
    
    #math fsk[status][skill_idx] {$fsk[status][skill_idx] + 1};
    
    #if {$fsk[status][skill_idx] > $fsk[config][skill_count]} {
        _fskmsg 所有技能升级完成;
        
    } {
        _fskmsg 准备下一个技能;
        #var fsk[status][base] $fsk[skills][$fsk[status][skill_idx]][base];
        #var fsk[status][special] $fsk[skills][$fsk[status][skill_idx]][special];
        
        #if {"$fsk[status][base]" != "force"} {
            jifa $fsk[status][base] $fsk[status][special];
        };
        
        cb_on_next_skill;
    };
};

#var _on_sleep_way 0;
#alias {_go_sleep} {
    #if {$_on_sleep_way == 0} {
        #var _on_sleep_way 1;
        
        #undelay {wd_lian};
        #undelay {wd_lingwu};
        #event {END OF PATH} {
            #untic {tk_walk};
            set brief 0;
            #delay {1} {_fskmsg 到达目的地};
            #unevent {END OF PATH};
        };
        set brief 1;
        #path load $fsk[config][path];
        #tic {tk_walk}{#path walk}{1};
        on_there {
            #var _on_sleep_way 0;
            sleep;
        };
    };
};
#alias {_go_fsk} {
    #alias cb_go_fsk #cr;
    #alias cb_go_fsk %0;
    
    #event {END OF PATH} {
        #untic {tk_walk};
        set brief 0;
        #delay {1} {_fskmsg 到达目的地};
        #unevent {END OF PATH};
    };
    set brief 1;
    #path load $fsk[config][rpath];
    #tic {tk_walk}{#path walk}{1};
    on_there {
        cb_go_fsk;
        #unalias cb_go_fsk
    };
};
#alias {_lian} {
    #delay {wd_lian} {_lian} {5};
    
    #send {lian $fsk[status][base] $fsk[config][freq][lx]};
};
#alias {_lingwu} {
    #delay {wd_lingwu} {_lingwu} {5};
    
    #send {lingwu $fsk[status][base] $fsk[config][freq][lw]};
};
#alias {lingw} {
    unwield all;
    
    #class lingw_inner open;
    
    #act {{你的基本功夫比你的高级功夫还高|你已经达到你的技能等级极限}} {
        _fskmsg 需要练习;
        #class lingw_inner kill;
        #undelay {wd_lingwu};
        #if {"$fsk[status][base]" == "force"} {
            fsk stop;
            _fskmsg 基本内功达到上限;
            on_next_skill {
                lianx;
            };
        } {
            lianx;
        };
    };
    #act {^你领悟了%*次} {
        hpbrief1;
        #delay {dl_next_lingw} {
            #if {$status[sp] < 50} {
                yun regenerate;
                #if {$status[water] < @eval{$status[water_max] * 0.9}} {
                    do 3 he jiudai;
                };
            } {
                _lingwu;
            };
        }{1};     
    };
    #act {^你略一凝神} {
        _lingwu;
    };
    #act {你现在过于疲倦,无法静下心来领悟功夫} {
        hpbrief1;
        he jiudai;eat liang;
        #delay {1} {
            #if {$status[force] > @eval{$status[force_max] * 0.2}} {
                yun regenerate;
            } {
                _go_sleep;
            };
        };
    };
    #act {你刚在三分钟内睡过一觉} {
        _fskmsg 5秒后再尝试睡觉;
        #delay {dl_lingwu_sleep} {
            sleep;
        } {5};
    };
    #act {不一会儿，你就进入了梦乡} {
        #delay {wd_wakeup} {
            _go_fsk { _lingwu };
        }{100};
    };
    #act {你一觉醒来，精神抖擞地活动了几下手脚} {
        #cr;
        #undelay {wd_wakeup};
        _fskmsg 睡觉醒来去领悟;
        #delay {1} {
            _go_fsk { _lingwu };
        };
    };
    #act {你的内力不够} {
        #undelay {dl_next_lingw};
        _fskmsg 内力不够去睡觉;
        he jiudai;eat liang;
        #delay {1} {_go_sleep};
    };
    
    #class lingw_inner close;
    
    #delay {1.5}{_lingwu};
};
#alias {_check_skill_level} {
    #act {^│技能级别%s　%d%s　技能等级%s　%S%s　技能类别%s　%S│} {
        #var fsk[status][level] %%2;
        #unact {^│技能级别%s　%d%s　技能等级%s　%S%s　技能类别%s　%S│};
    };
    #send {verify $fsk[status][special]};
};
#alias {lianx} {
    
    #class lianx_inner open;
    
    #nop 特殊内功无法练习;
    
    #act {你需要提高基本功} {
        _check_skill_level;
        #delay {1} {
            #if {$fsk[status][level] < $fsk[config][limit]} {
                _fskmsg 需要领悟;
                #class lianx_inner kill;
                #undelay {wd_lian};
                lingw;
            } {
                fsk stop;
                _fskmsg 当前技能达到上限;
                
                on_next_skill {
                    lianx;
                };
            };
        };
    };
    #act {你的基本%*的级别还没有%*的级别高，不能通过练习来提高了} {
        _fskmsg 需要领悟;
        #class lianx_inner kill;
        #undelay {wd_lian};
        lingw;
        #unalias cb_lianx;
    };
    #act {^你%*练习%*获得了%*进步} {
        hpbrief1;
        #delay {dl_next_lianx} {
            #if {$status[hp] < 50} {
                yun recover;
            } {
                _lian;
            };
        }{1};        
    };
    #act {^你深深吸了几口气，脸色看起来好多了。} {
        _lian;
    };
    #nop #act {你的体力太低了};
    #act {{你的体力太低|你的体力太差|你的体力不够|你现在气血虚浮|你太累了}} {
        hpbrief1;
        he jiudai;eat liang;
        #delay {1} {
            #if {$status[force] > @eval{$status[force_max] * 0.2}} {
                yun recover
            } {
                _go_sleep;
            };
        };
    };
    #act {你刚在三分钟内睡过一觉} {
        _fskmsg 5秒后再尝试睡觉;
        #delay {dl_lian_sleep} {
            sleep;
        } {5};
    };
    #act {不一会儿，你就进入了梦乡} {
        #delay {wd_wakeup} {
            _go_fsk { _lian };
        }{100};
    };
    #act {你一觉醒来，精神抖擞地活动了几下手脚} {
        #cr;
        #undelay {wd_wakeup};
        _fskmsg 睡觉醒来去练功;
        #delay {1} {
            _go_fsk { _lian };
        };
    };
    #act {你的内力不够} {
        #undelay {dl_next_lianx};
        _fskmsg 内力不够去睡觉;
        he jiudai;eat liang;
        #delay {1} {_go_sleep};
    };
    
    #class lianx_inner close;
    
    unwield all;
    #delay {1.5} {
        #if {"$fsk[status][base]" == "force"} {
            fsk stop;
            lingw;
        } {
            #switch {"$fsk[status][base]"} {
                #case {"sword"} {
                    wield sword;
                };
                #case {"blade"} {
                    wield blade;
                };
                #case {"whip"} {
                    wield whip;
                };
                #case {"staff"} {
                    wield staff;
                };
                #case {"club"} {
                    wield club;
                };
            };
            #delay {1} {_lian};
        };
    };
};
#alias {fsk} {
    #switch {"%1"} {
        #case {"start"} {
            
            #var fsk[flag][mode] %2;
            #var fsk[status][skill_idx] 0;
            
            on_next_skill {
                lianx;
            };
        };
        #case {"stop"} {
            #untic {tk_walk};
            #undelay {wd_lian};
            #undelay {wd_lingwu};
            #undelay {wd_wakeup};
            #undelay {dl_next_lianx};
            #undelay {dl_next_lingw};
            #class lianx_inner kill;
            #class lingw_inner kill;
        };
        #case {""} {
            mod fullskill help;
        };
        #default {
        
        };
    };
};

#class $mod_name close;

${mod_name}_load;
