#class $mod_name open;

#alias {${mod_name}_load} {
    #showme Module $mod_name loaded;
    #unalias ${mod_name}_load;
};

#alias {${mod_name}_unload}{
    map stop;
    #class $mod_name kill;
    #showme Module $mod_name unloaded;
};

#nop 红花老祖本姓朱 为救苍生下凡来;

#nop ============ Module variables start ======================================;

#var map[status][mapping] 0;

#var map[room][name] {};
#var map[room][exit] {};
#var map[room][desc] {};
#var map[room][area] {};
#var map[room][unit] {};
#var map[room][item] {};
#var map[room][indoor] 0;
#var map[room][season] {};
#var map[room][weather] {};

#var map[gt][status] noaction;
#var map[gt][path] {};

#nop ============ Module funtion start ========================================;

#function {format_exits} {
    #var format_exits_inner[estr] %1;
    
    #replace {format_exits_inner[estr]} {。} {};
	#replace {format_exits_inner[estr]} { } {};
	#replace {format_exits_inner[estr]} {和} { };
	#replace {format_exits_inner[estr]} {、} { };
	
	#replace {format_exits_inner[estr]} {northeast} {ne};
	#replace {format_exits_inner[estr]} {northwest}{nw};
	#replace {format_exits_inner[estr]} {southeast}{se};
	#replace {format_exits_inner[estr]} {southwest}{sw};
    #replace {format_exits_inner[estr]} {northdown}{nd};
    #replace {format_exits_inner[estr]} {southdown}{sd};
    #replace {format_exits_inner[estr]} {eastdown}{ed};
    #replace {format_exits_inner[estr]} {westdown}{wd};
    #replace {format_exits_inner[estr]} {southup}{su};
    #replace {format_exits_inner[estr]} {northup}{nu};
	#replace {format_exits_inner[estr]} {eastup}{eu};
    #replace {format_exits_inner[estr]} {westup}{wu};
	#replace {format_exits_inner[estr]} {south}{s};
	#replace {format_exits_inner[estr]} {north}{n};
    #replace {format_exits_inner[estr]} {down}{d};
	#replace {format_exits_inner[estr]} {east}{e};
	#replace {format_exits_inner[estr]} {west}{w};
    #replace {format_exits_inner[estr]} {up}{u};
    
    #list result create $format_exits_inner[estr];
    #unvar format_exits_inner;
};

#nop ============ Module private function start ===============================;

#alias {_hnd_roomdesc} {
    #regexp {%1} {^%S%s-%s$} {
        #nop #send {get all};
        #var hnd_addesc_inner[desc_start] 1;
        #var hnd_addesc_inner[desc] {};
        #var map[room][unit] {};
        #var map[room][indoor] 1;
        #var map[room][season] {};
		#var map[room][weather] {};
		#var map[room][item] {};
        #var map[room][name] &1;
    } {
    #nop dbg $hnd_addesc_inner[desc_start];
        #if {&hnd_addesc_inner[desc_start] != 0} {
            #regexp {%1} {^%s这里%*{方向|出口}{是|有}%*} {
                #var hnd_addesc_inner[desc_start] 0;
                #var map[room][exit] @format_exits{&5};
            };
            #regexp {%1} {^%s「%*」: %*。$} {
                #var hnd_addesc_inner[desc_start] 0;
                #var map[room][indoor] 0;
				#var map[room][season] &2;
				#var map[room][weather] &3;
            };
            #regexp {%1}{^%s你可以看看(look):%*$}
			{
				#nop #var hnd_addesc_inner[desc_start] 0;
				#var map[room][item] {&2};

				#replace {map[room][item]}{,}{;};
			};
            #if {$hnd_addesc_inner[desc_start] == 1} {
                #var hnd_addesc_inner[desc] @trim{$hnd_addesc_inner[desc]%1};
                #unvar result;
            };
            #regexp {%1} {^%s%S「%S」%S(%w %w)%*}
			{
				#var map[room][unit][&5 &6] &4;
			}{
				#regexp {%1} {^%s%S(%w %w)%*}
				{
					#var map[room][unit][&3 &4] &2;
				}{
					#regexp {%1} {^%s%S%s%S(%w %w)%*}
					{
						#var map[room][unit][&5 &6] &4;
					}{
						#regexp {%1}{^%s%S%s%S%s%S(%w %w)%*}
						{
							#var map[room][unit][&7 &8] &6;
						};
					};
				};
			};
			
			#nop exclude items in units;
			#unvar map[room][unit][Huo tong];
			#unvar map[room][unit][Yun tie];
			#unvar map[room][unit][Shi zi];
			#unvar map[room][unit][Xiuhua bengjia];
			#unvar map[room][unit][Jin hua];
			
            #regexp {%1} {^>%s} {
                #var map[room][desc] $hnd_addesc_inner[desc];
                #unvar hnd_addesc_inner;
                #showme <139>->_hnd_roomdesc : Get room desc complete<079>;
                #showme ->;
            }; 
        };
    };
};

#nop ============ Module alias start ==========================================;

#alias {on_exit_blocked} {
    #alias cb_on_exit_blocked #cr;
    #alias cb_on_exit_blocked %0;
    
    #act {你的动作还没有完成，不能移动} {
        cb_on_exit_blocked;
    };
    #act {官兵们拦住了你的去路} {
        cb_on_exit_blocked;
    };
};

#alias {map} {
    #switch {"%1"} {
        #case {"load"} {
            #map read data/pku.map;
            #map return;
            map pad;
            dbg map data loaded from data/pku.map;
        };
        #case {"save"} {
            #map write data/pku.map;
            
            dbg map data saved to data/pku.map;
        };
        #case {"reload"} {
            #map destroy;
            map load;
            dbg map data reloaded from data/pku.map;
        };
        #case {"stop"} {
            event_handler_del {_hnd_roomdesc} {RECEIVED LINE};
            #if {$map[status][mapping] == 1} {
                #var map[status][mapping] 0;
                ades stop;
                
                #class map_start_inner kill;
            };
            #class ad_inner kill;
            #nop mod bag load;
            dbg mapping stopped;
        };
        #case {"pad"} {
            #alias {mappad} {
                #map goto {$map[room][name]}{}{$map[room][desc]}{$map[room][area]};
                show_room_data;
                #unalias mappad;
                dbg map padding complete;
            };
            #if {$map[status][mapping] == 0} {
                get_area {
                    on_get_room {
                        mappad;
                    };
                };
            };
            #elseif {$map[status][mapping] == 1} {
                get_area {
                    mappad;
                };
            };
            
        };
        #case {"start"} {
            
            #nop mod bag unload;
            get_area;
            dbg start mapping;
            
            #var map[status][mapping] 1;
            
            event_handler_add {_hnd_roomdesc} {RECEIVED LINE};
            
            #class map_start_inner open;
            
            #var ades 0;
            
            #act {_hnd_roomdesc : Get room desc complete} {
                #nop get_area;
                #if {$adnew == 1} {
                    #undelay ad_inner_watchdog;
                    
                    #map dig {$chukou} {new};
                    #map move $chukou;
                    #map name $map[room][name];
                    #map set roomarea $map[room][area];
                    #map set roomdesc $map[room][desc];
                    #map set roomdata $map[room][unit];
                    
                    #var timeout 0;
                    #class ad_inner kill;
                };
                #if {$ades == 1} {
                    ades;
                    
                };
                
                show_room_data;
            };
            #alias {ada} {
                #if {"%%1" != ""} {
                    #var map[room][area] %%1;
                    #showme <139>->room area been changed to : %%1<079>;
                } {
                    #showme <139>->current area : $map[room][area]<079>;
                };
            };
            #alias {ades} {
                #if {"%%1" == "start"} {
                    #var ades 1;
                    dbg roomdesc auto-store starting;
                };
                #elseif {"%%1" == "stop"} {
                    #var ades 0;
                    dbg roomdesc auto-store stopped;
                };
                #else {
                    #map name $map[room][name];
                    #map set roomdesc $map[room][desc];
                    #map set roomarea $map[room][area];
                    #map set roomdata $map[room][unit];
                    
                    #nop #map get;
                    dbg room data updated : area $map[room][area];
                };
            };
            #alias {pre} {
                #if {"%%1" != ""} {
                    
                    
                    #var pre[exit] %%1;
                    #map flag nofollow;
                    #send $pre[exit];
                    
                    #delay {2} {
                        #map find {$map[room][name]}{}{$map[room][desc]};
                        #path save f pre[path];
                        #if {&pre[path] != 0} {
                            #replace pre[path] {\;} { };
                            #list pre[path] create $pre[path];
                            
                            #map get roomvnum pre[lastvnum];
                            
                            #list pre[path] size pre[pathlen];
                            #loop 1 $pre[pathlen] i {
                                #map move $pre[path][$i];
                            };
                            #unvar i;
                            #map get roomvnum pre[targetvnum];
                            #map get roomexits pre[targetexits];
                            #list pre[targetexits] create $pre[targetexits][];
                            #map goto $pre[lastvnum];
                            
                            #if {0 == @isListMember{pre[targetexits];@reverse{$pre[exit]}}} {
                                dbg 出口 $pre[exit] 存在房间记录 $pre[targetvnum];
                            };
                        } {
                            
                            dbg 出口 $pre[exit] 还没有房间记录; 
                        };
                        #send @reverse{$pre[exit]};
                        #map flag nofollow;
                        #unvar pre;
                    };
                    
                    
                } {
                    #showme <129> Usage : pre <direction><079>;
                };
            };
            #alias {ad} {
                #undelay ad_inner_watchdog;
                
                #class ad_inner open;
                
                #var adnew 1;
                #var chukou %%1;
                #var timeout 1;
                
                #delay {ad_inner_watchdog} {
                    #if {$timeout == 1} {
                        #class ad_inner kill;
                        #showme <139>->ad timeout<079>;
                    };
                } {3};
                #act {这个方向没有出路} {
                    #undelay ad_inner_watchdog;
                    #class ad_inner kill;
                    #showme <139>->ad no exit<079>;
                };
                #class ad_inner close;
                
                #send $chukou;
            };
            #class map_start_inner close;
        };
        #default {
            #showme <129>Usage : map <load | pad | save | reload | start | stop><079>;
        };
    };
};

#alias {on_map_pad} {
    #alias cb_on_map_pad #cr;
    #alias cb_on_map_pad %0;
    
    #act {^->map padding complete} {
        #unact {^->map padding complete};
        #delay {1} {cb_on_map_pad};
    };
    
    map pad;
};
#alias {on_handle_roomdesc} {
    #alias cb_on_handle_roomdesc #cr;
    #alias cb_on_handle_roomdesc %0;
    
    #act {_hnd_roomdesc : Get room desc complete} {
        cb_on_handle_roomdesc;
    };
};
#alias {on_get_room} {
    #class on_get_room_inner open;
    
    #alias cb_get_current_room #cr;
    #alias cb_get_current_room %0;
    
    event_handler_add {_hnd_roomdesc} {RECEIVED LINE};
    on_handle_roomdesc {
        event_handler_del {_hnd_roomdesc} {RECEIVED LINE};
        cb_get_current_room;
        #class on_get_room_inner kill;
    };
    
    #class on_get_room_inner close;
    #send look;
};
#alias {get_area} {
    #class get_area_inner1 open;
    
    
    #alias {get_area_end} {
        #class get_area_inner1 kill;
        #delay {dl_get_area} {
            get_area_map_end;
        } {2};    
    };
    #act {这里暂时没有地图} {
        #var map[room][area] {};
        get_area_end;        
        
        dbg 当前房间没有地图信息
    };
    #act {^【%*地图】} {
        #var map[room][area] %%1;
        get_area_end;
    };
    #act {^【%*地图－%*】} {
        #var map[room][area] %%1%%2;
        #nop #var map[room][area2] %%2;
        get_area_end;
    };
    #class get_area_inner1 close;
    
    #class get_area_inner2 open;
    #alias cb_get_area #cr;
    #alias cb_get_area %0;
    #alias {get_area_map_end} {
        cb_get_area;
        #class get_area_inner2 kill;
        dbg area changed to : $map[room][area];
    };
    #act {== 未完继续} {
        #send q;
    };
    #act {[临时存储讯息]} {
        #undelay {dl_get_area};
        get_area_map_end;
    };
    #class get_area_inner2 close;
    #send localmaps;
};
#alias {show_room_data} {
    #map get roomexits tmp_room_data[knownexit];
    #list tmp_room_data[knownexit] create $tmp_room_data[knownexit][];
    
    #foreach {$map[room][exit][%*]} {eett} {
        #list tmp_room_data[knownexit] find {$eett} idx;
        #if {$idx == 0} {
            #list tmp_room_data[unknownexit] add $eett;
        }; 
    };

    #showme ->;
    #showme <139>->房间出口:$map[room][exit]<079>;
    #showme <119>->未知出口:$tmp_room_data[unknownexit]<079>;
    #showme <129>->已知出口:$tmp_room_data[knownexit]<079>;
    #map get roomarea tmp_room_data[area];
    #map get roomname tmp_room_data[name];
    #map get roomdesc tmp_room_data[desc];
    #showme <129>->已知描述:$tmp_room_data[area] => $tmp_room_data[name] => $tmp_room_data[desc]<079>;
    #nop #showme ->$map[room][area] => ${map[room][name]} => ${map[room][desc]};
    #showme ->;
    
    #unvar tmp_room_data;#unvar eett;#unvar idx;
};

#alias {carto} {
    #if {"%2" == ""} {
        #map dig to%1;
    } {
        #map link to%1 %2;
    };
    #map exit to%1 COMMAND {qu %1};
};

#alias {pathwalk} {
    #class pathwalk_inner open;
    
    #act {^%S%s-%s$} {
        #if {"%%1" !== "马车"} {
            #path walk;
        };
    };
    
    #class pathwalk_inner close;
    
    #path walk;
};

#nop 你一进戒律院，...只听几声大喝，如同炸雷般在大殿里回响  #map goto 1466;
#nop 执法僧兵说道：「戒律院玄痛大师请你去陈述此次下山经过 ！」;
#nop 你战战兢兢，晃晃悠悠地向对面走去......;
#nop 你战战兢兢，晃晃悠悠地向对面走去......;
#nop 你终于来到了对面，心里的石头终于落地。;

#nop 一叶扁舟缓缓地驶了过来，艄公将一块踏脚板搭上堤岸，以便乘客;
#nop 岸边一只渡船上的老艄公说道：正等着你呢，上来吧。;
#nop 唐三杰道：你老人家快请上船;
#nop 唐三杰接过你递给的船资一两白银五十文铜板;

#nop ]q向上官八富打听有关『过河』的消息。;
#nop 上官八富鼓足中气，长啸一声：“船家！”;
#nop 一叶扁舟缓缓地驶了过来，艄公将一块踏脚板搭上堤岸，以便乘客;
#nop 上下。;
#nop ]q向上官八富打听有关『过江』的消息。;
#nop 上官八富不耐烦的瞪了]q一眼，说道：没看我正忙着吗？;
#nop ]q吸了口气，一声“船家”，声音中正平和地远远传了出去。;
#nop 岸边一只渡船上的老艄公说道：正等着你呢，上来吧。;
#nop ]q往里离开。;

#nop 你只觉得离塘沽口越来越远，不一会儿就再也看不见了。;
#nop 你感觉过了好长时间，正在昏昏欲睡间，感觉船震了一下，原来是靠岸了。;

#nop 你级别不够，不能进入达摩院
#nop 你的动作还没有完成，不能移动;
#nop 你不小心被什么东西绊了一下，差点摔个大跟头;

#alias {on_there} {
    #alias cb_on_there #cr;
    #alias cb_on_there %0;
    
    #act {到达目的地} {
        #unact {到达目的地};
        cb_on_there;
        #unalias cb_on_there;
    };
};
#alias {on_error} {
    #alias cb_on_error #cr;
    #alias cb_on_error %0;
    
    #act {行走出错} {
        #unact {行走出错};
        cb_on_error;
        #unalias cb_on_error;
    };
};
#alias {mess} {
    #message  ACTIONS               %1;
    #message  ALIASES               %1;
    #message  CLASSES               %1;
    #message  COMMANDS              %1;
    #message  CONFIGURATIONS        %1;
    #message  DELAYS                %1;
    #message  EVENTS                %1;
    #message  FUNCTIONS             %1;
    #message  GAGS                  %1;
    #message  HIGHLIGHTS            %1;
    #message  HISTORIES             %1;
    #message  MACROS                %1;
    #message  PATHS                 %1;
    #message  PATHDIRS              %1;
    #message  PROMPTS               %1;
    #message  SUBSTITUTIONS         %1;
    #message  TABS                  %1;
    #message  TICKERS               %1;
    #message  VARIABLES             %1;
};

#alias {goto_stop} {
    set brief 0;
    gt_wrn 行走停止;
    #class go_forward_inner kill;
    #class _path_run_inner kill;
    #class goto_inner kill;
    #var map[gt][status] 0;
};

#alias {goto} {
    #nop param1 roomname;
    #nop param2 roomarea;
    
    #if {$map[gt][status] == 1} {
        #return;
    };
    #elseif {$map[gt][status] == 0} {
        #var map[gt][status] 1;
    };
    
    mess off;
    
    #var map[gt][dest][name] {%1};
    #var map[gt][dest][area] {%2};
    
    #class goto_inner open;
    
    #alias {gt_err} {
        #showme <119> GPS : %%0<079>;
    };
    #alias {gt_msg} {
        #showme <129> GPS : %%0<079>;
    };
    #alias {gt_wrn} {
        #showme <139> GPS : %%0<079>;
    };
    
    #var path_has_find 0;
    #alias {path_save} {
        #path save forward map[gt][path];
        #if {"$map[gt][path]" != ""} {
            #var path_has_find 1;
        };
    };
    
    #class goto_inner close;

    #nop path finding;
    
    #if {"%1" == "" && "%2" == ""} {
        gt_msg goto <roomname> [roomarea];
        #var map[gt][status] 0;
    } {
        #map get roomname map[room][name];
        
        #var map[gt][path] {};
        
        #if {"%2" == ""} {
            #if {0 == $path_has_find} {
                #map find {$map[gt][dest][name]};
                path_save;
            };
            #if {0 == $path_has_find} {
                #map find {%*$map[gt][dest][name]%*};
                path_save;
            };
            #if {0 == $path_has_find} {
                #map find {}{}{}{}{$map[gt][dest][name]};
                path_save;
            };
            #if {0 == $path_has_find} {
                #map find {}{}{}{}{%*$map[gt][dest][name]%*};
                path_save;
            };
        
        } {
            #if {0 == $path_has_find} {
                #map find {$map[gt][dest][name]} {} {} {$map[gt][dest][area]};
                path_save;
            };
            #if {0 == $path_has_find} {
                #map find {%*$map[gt][dest][name]%*} {} {} {$map[gt][dest][area]};
                path_save;
            };
            #if {0 == $path_has_find} {
                #map find {%*$map[gt][dest][name]%*}{}{}{%*$map[gt][dest][area]%*};
                path_save;
            };
            #if {0 == $path_has_find} {
                #map find {} {} {} {$map[gt][dest][area]} {$map[gt][dest][name]};
                path_save;
            };
            #if {0 == $path_has_find} {
                #map find {} {} {} {$map[gt][dest][area]} {%*$map[gt][dest][name]%*};
                path_save;
            };
            #if {0 == $path_has_find} {
                #map find {} {} {} {%*$map[gt][dest][area]%*} {%*$map[gt][dest][name]%*};
                path_save;
            };
            #if {0 == $path_has_find} {
                path_save;
            };
        
        };
    
        mess on;

        #if {0 == $path_has_find} {
            gt_wrn 目标地点不可达;
            #class goto_inner kill;
            #var map[gt][status] 0;        
        } {
            gt_msg 找到路径 {$map[gt][path]};

            on_error {
                #var map[gt][status] 0;
                #class goto_inner kill;
            };
            
            _path_run;
        };
    };
    
};

#alias {_path_run} {
    #class _path_run_inner open;
    
    #var path_run {};
    #list path_run[list] add $map[gt][path];
    #list path_run[list] size path_run[list_len];
    #var path_run[list_idx] 0;
    
    #alias {back_one_step} {
        #math path_run[list_idx] {$path_run[list_idx] - 1};
    };
    #alias {go_forward} {
        
        #math path_run[list_idx] {$path_run[list_idx] + 1};
        #if {$path_run[list_idx] > $path_run[list_len]} {
            #class _path_run_inner kill;
            #class on_blocked_inner kill;
            #class on_rewalk_need_inner kill;
            
            set brief 0;
            gt_msg 到达目的地;
            #var map[gt][status] 0;
            #class goto_inner kill;
        } {
            #class go_forward_inner open;
            
            on_blocked {
                #map undo;
                
                #class go_forward_inner kill;
                #class _path_run_inner kill;
                #class on_rewalk_need_inner kill;
                
                set brief 0;
                
                gt_wrn 行走出错;
            };
            on_rewalk_need {
                #nop #show 需要重走 $path_run[list][$path_run[list_idx]];
                #map undo;
                back_one_step;
                #delay {1} {
                    go_forward;
                };
            };
            
            #class go_forward_inner close;
            
            #if {"$path_run[list][$path_run[list_idx]]" == "to%*"} {
                on_car_arrived {
                    on_walked {
                        #class go_forward_inner kill;
                        go_forward;
                    };
                    xia;
                };
            };
            #elseif {"$path_run[list][$path_run[list_idx]]" == "%*boat"} {
                on_boat_arrived {
                    on_walked {
                        #undelay {wd_boat_arrived};
                        #class go_forward_inner kill;
                        go_forward;
                    };
                    #delay {wd_boat_arrived}{look}{5};
                    out;
                    #nop go_forward;
                };
            };
            #elseif {"$path_run[list][$path_run[list_idx]]" == "climb%*" ||
                     "$path_run[list][$path_run[list_idx]]" == "guo%*" ||
                     "$path_run[list][$path_run[list_idx]]" == "zou%*" } {
                on_action_done {
                    on_walked {
                        #class go_forward_inner kill;
                        go_forward;
                    };
                    #nop go_forward;
                };
            };
            #else {
                on_walked {
                    #class go_forward_inner kill;
                    go_forward;
                };
            };
            
            #nop #show $path_run[list][$path_run[list_idx]];
            $path_run[list][$path_run[list_idx]];
        };
        
    };
    #alias {on_walked} {
        #alias cb_on_walked #cr;
        #alias cb_on_walked %%0;
        
        #act {^%S%s-%s$} {
            #unact {^%S%s-%s$};
            cb_on_walked;
        };
    };
    #alias {on_blocked} {
        #alias cb_on_blocked #cr;
        #alias cb_on_blocked %%0;
        
        #class on_blocked_inner open;
        
        #list on_blocked_trigger_list create {
            ^这个方向没有出路;
            ^哎哟，你一头撞在墙上;
            ^你刚要前行，忽然发现江水决堤，不由暗自庆幸，还好没过去;
            ^你正要前行，有人大喝：黄河决堤啦，快跑啊;
            ^你从悬崖上摔了下来;
            ^那条路不知深浅，还是不要去了;
            ^你身上没有车资%*车夫拒绝开车;
            ^陆大有喝道：你不是华山弟子，不得入内;
            ^童百熊说道：「你不是我日月神教弟子，来我教干什么？速速离开！」;
            ^江百胜伸手拦住你说道：盟主很忙，现在不见外客，你下山去吧;
            ^清兵说道：赶快滚开，这是你停留的地方吗;
            ^守山弟子说: 非我派弟子不许上峨嵋山;
            ^守山弟子说: 峨嵋非行凶之地，施主想要上山请解兵器;
            ^守卫喝道：闲杂人等，不得乱闯;
            ^褚万里喝道：闲杂人等，不得入内;
            ^王府亲兵大声喝道：「哪里来的刁民，胆敢擅闯王府？」;
            ^校尉挡住了你的去路：大将军不在府上;
            ^梅剑伸手拦住你，说道：“非灵鹫宫弟子请回！”;
            ^壮年僧人说道：这位施主请回罢，本寺不接待俗人;
            ^石阶越来越险，你提心吊胆的走着，终于一个不小心滚了下去;
            ^厚土旗众大声吼道：“你不是明教弟子，不许上山！”;
            ^明教休息室，外派弟子不得入内;
            ^官兵拦住你说道;
            ^就凭你这点武功，最好不要瞅热闹了，小心被潮水卷跑了;
            ^你身上背的东西太重，洞口又太高，你够不着;
            ^红花会众说道：“阁下不是红花会弟子，最好不要进去;
            ^你觉得贸然进去有些不妥，还是先问问傻姑和程英再说吧;
        };
        
        #foreach {$on_blocked_trigger_list[%*]} {regstring} {
            #act {$regstring} {
                #class on_blocked_inner kill;
                cb_on_blocked;
            };
        };
        
        #class on_blocked_inner close;
    };
    #alias {on_rewalk_need} {
        #alias cb_on_rewalk_need #cr;
        #alias cb_on_rewalk_need %%0;
        
        #class on_rewalk_need_inner open;
        
        #list on_rewalk_need_trigger_list create {
            ^吊桥还没有升起来，你就这样走了，可能会给外敌可乘之机的;
            ^你的动作还没有完成，不能移动;
            ^僧兵上前把大门关了起来;
            ^乒地一声，里面有人把大门关上了;
            ^只听得江面上隐隐传来：“别急嘛，这儿正忙着呐;
            ^你逃跑失败;
            ^沙石地几乎没有路了，你走不了那么快;
            ^你小心翼翼往前挪动，遇到艰险难行处，只好放慢脚步;
            ^你还在山中跋涉，一时半会恐怕走不出这六盘山;
            ^你还在山中跋涉，一时半会恐怕走不出这西南地绵绵群山;
            ^你还在山中跋涉，一时半会恐怕走不出这藏边群山;
            ^青海湖畔美不胜收，你不由停下脚步，欣赏起了风景;
            ^你不小心被什么东西绊了一下，差点摔个大跟头;
        };
        
        #foreach {$on_rewalk_need_trigger_list[%*]} {regstring} {
            #act {$regstring} {
                #nop #classon_rewalk_need_inner kill;
                cb_on_rewalk_need;
            };
        };
        
        #class on_rewalk_need_inner close;
        
    };
    
    #alias {on_action_done} {
        #alias cb_on_action_done #cr;
        #alias cb_on_action_done %%0;
        
        #class on_action_done_inner open;
        
        #list on_action_done_trigger_list create {
            ^突然间蓬一声，屁股撞上了什么物事，身子向上弹起，原来恰好撞到崖边伸出的一株;
            ^一条大瀑布如玉龙悬空，滚滚而下，倾入一座清澈异常的大湖之中;
            ^你终于一步步的终于挨到了桥头;
            ^你终于来到了对面，心里的石头终于落地;
            ^你觉得好象停了下来，睁开眼一看;
        };
        
        #foreach {$on_action_done_trigger_list[%*]} {regstring} {
            #act {$regstring} {
                #class on_action_done_inner kill;
                cb_on_action_done;
            };
        };
        
        #class on_action_done_inner close;
    };
    #alias {on_car_arrived} {
        #alias cb_on_car_arrived #cr;
        #alias cb_on_car_arrived %%0;
        
        #act {^大车停稳了下来，你可以下车(xia)了} {
            #unact {^大车停稳了下来，你可以下车(xia)了};
            cb_on_car_arrived;
        };
    };
    #alias {on_boat_arrived} {
        #alias cb_on_boat_arrived #cr;
        #alias cb_on_boat_arrived %%0;
        
        #class on_boat_arrived_inner open;
        
        #nop 船夫对你说道：“到了，上岸吧。”，随即把一块踏脚板搭上堤岸;
        #nop 你感觉过了好长时间，正在昏昏欲睡间，感觉船震了一下，原来是靠岸了;
        #list boat_arrived_trigger_list create {
            ^艄公说“到啦，上岸吧”，随即把一块踏脚板搭上堤岸;
            ^你沿着踏板走了上去;
            ^你朝船夫挥了挥手便跨上岸去;
            ^不知过了多久，船终于靠岸了，你累得满头大汗;
            ^小舟终于划到近岸，你从船上走了出来;
            ^绿衣少女将小船系在树枝之上，你跨上岸去;
        };
        
        #foreach {$boat_arrived_trigger_list[%*]} {regstring} {
            #act {$regstring} {
                #class on_boat_arrived_inner kill;
                cb_on_boat_arrived;
            };
        };
        
        #class on_boat_arrived_inner close;
    };
    #class _path_run_inner close;
    
    set brief 1;
    go_forward;
};

#list area_list create {建康府诸军都统制府;临安府西湖;临安提督府;少林寺寺内;北京城内城;长江北岸;长江南岸;建康府南;建康府北;黄河南岸;黄河北岸;丝绸之路;回族小镇;日月神教;康亲王府;平西王府;扬州城;镇江府;临安府;牙山湾;苏州城;峨嵋派;归云庄;大理城;桃源县;杀手帮;桃花岛;铁掌峰;岳王墓;洛阳城;长安城;大轮寺;凌霄城;侠客岛;冰火岛;华山村;洛阳东;麒麟村;晋阳城;兰州城;湟中城;星宿海;灵鹫宫;全真教;武当山;紫禁城;神龙岛;天龙寺;张家口;无量山;嘉兴;明州;江州;慕容;泉州;岳阳;梅庄;闽南;灵州;南昌;信阳;荆州;襄阳;华山;丐帮;中原;古墓;曲阜;泰山;天坛;回部;明教;白驼;成都;昆明;苗疆}

#alias {gotowork} {
    #var work_place %1;
    
    #replace {work_place} {杭州提督府} {临安提督府};
    #replace {work_place} {建康府南城} {建康府南};
    #replace {work_place} {建康府北城} {建康府北};
    #replace {work_place} {长江长江} {长江南岸长江};
    #replace {work_place} {牙山牙山} {牙山湾牙山};
    #replace {work_place} {大理城中} {大理城};
    #replace {work_place} {姑苏慕容} {慕容};
    #replace {work_place} {小山村} {华山村};
    #replace {work_place} {少林寺} {少林寺寺内};
    #replace {work_place} {白驼山} {白驼};
    #replace {work_place} {福州} {闽南};
    #replace {work_place} {扬州} {扬州城};
    #replace {work_place} {镇江} {镇江府};
    #replace {work_place} {西湖} {临安府西湖};
    #replace {work_place} {苏州} {苏州城};
    #replace {work_place} {峨嵋} {峨嵋派};
    #replace {work_place} {桃源} {桃源县};
    #replace {work_place} {洛阳} {洛阳城};
    #replace {work_place} {长安} {长安城};
    #replace {work_place} {晋阳} {晋阳城};
    #replace {work_place} {北京} {北京城内城};
    #replace {work_place} {兰州} {兰州城};
    #replace {work_place} {湟中} {湟中城};
    #replace {work_place} {星宿} {星宿海};
    #replace {work_place} {灵鹫} {灵鹫宫};
    #replace {work_place} {全真} {全真教};
    
    #foreach {$area_list[%*]} {areaname} {
        #regexp {$work_place}{^$areaname%*} {
        
            #if {"&1" == "红娘%*"} {
                goto &1;
            };
            #else {
                goto &1 $areaname;
            };
            
            log $areaname &1;
            
            #unvar areaname;#unvar work_place;
            #break;
        };
    };
};

#list bus_station_list create {11;20;21;30;45;46;64;94;135;168;186;197;243;255;285;321;796;985;989;991;1008;1010;1052;1075;1106;1108;1120;1181;1209;1532;1579;1580;1690;1740;2100;2200;2494;2532}

#alias {mapcar} {
    #map get roomvnum current_room_id;
    
    #if {"%1" == "on"} {
        #foreach {$bus_station_list[%*]} {room_id} {
            #map goto $room_id;
            #map roomflag avoid off;
        };
        #showme <139>GPS : 开启马车行节点<079>;
    };
    #elseif {"%1" == "off"} {
        #foreach {$bus_station_list[%*]} {room_id} {
            #map goto $room_id;
            #map roomflag avoid on;
        };
        #showme <139>GPS : 关闭马车行节点<079>;
    };
    
    #map goto $current_room_id;
    #unvar current_room_id;
};
#alias {on_get_exits} {
    #alias cb_on_get_exits #cr;
    #alias cb_on_get_exits %0;
    
    #act {^%s这里%*{方向|出口}{是|有}%*} {
        #unact {^%s这里%*{方向|出口}{是|有}%*};
        #var map[trvs][leaf] @format_exits{%%5};
        cb_on_get_exits;
    };
    look;
};

#alias {_visit} {
    #var return_path @reverse{%1};
    %1;
    #delay {1} {$return_path};
};
#alias {_trvs} {
    #if {"$map[trvs][parent]" == "rootnode"} {
        
    } {
    
    };
};
#alias {traverse} {
    #class traverse_inner open;
    
    #var map[trvs][need_deep] 1;
    #var map[trvs][need_deep] %1;
    #var map[trvs][curr_deep] 0;
    
    #var map[trvs][rootnode] {};
    #var map[trvs][leaf] {};
    #var map[trvs][parent] {rootnode};
    
    on_get_exits {
        
    };
    #class traverse_inner close;
};


#class $mod_name close;

${mod_name}_load;
