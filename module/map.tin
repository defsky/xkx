#class $mod_name open;

#alias {${mod_name}_load} {
    #showme Module $mod_name loaded;
    #unalias ${mod_name}_load;
};

#alias {${mod_name}_unload}{
    map stop;
    #class $mod_name kill;
    #showme Module $mod_name unloaded;
};

#nop 红花老祖本姓朱 为救苍生下凡来;

#nop ============ Module variables start ======================================;

#var map[status][mapping] 0;

#var map[room][name] {};
#var map[room][exit] {};
#var map[room][desc] {};
#var map[room][area] {};
#var map[room][unit] {};
#var map[room][item] {};
#var map[room][indoor] 0;
#var map[room][season] {};
#var map[room][weather] {};

#var map[gt][status] noaction;
#var map[gt][path] {};

#nop ============ Module funtion start ========================================;

#function {format_exits} {
    #var format_exits_inner[estr] %1;
    
    #replace {format_exits_inner[estr]} {。} {};
	#replace {format_exits_inner[estr]} { } {};
	#replace {format_exits_inner[estr]} {和} { };
	#replace {format_exits_inner[estr]} {、} { };
	
	#replace {format_exits_inner[estr]} {northeast} {ne};
	#replace {format_exits_inner[estr]} {northwest}{nw};
	#replace {format_exits_inner[estr]} {southeast}{se};
	#replace {format_exits_inner[estr]} {southwest}{sw};
    #replace {format_exits_inner[estr]} {northdown}{nd};
    #replace {format_exits_inner[estr]} {southdown}{sd};
    #replace {format_exits_inner[estr]} {eastdown}{ed};
    #replace {format_exits_inner[estr]} {westdown}{wd};
    #replace {format_exits_inner[estr]} {southup}{su};
    #replace {format_exits_inner[estr]} {northup}{nu};
	#replace {format_exits_inner[estr]} {eastup}{eu};
    #replace {format_exits_inner[estr]} {westup}{wu};
	#replace {format_exits_inner[estr]} {south}{s};
	#replace {format_exits_inner[estr]} {north}{n};
    #replace {format_exits_inner[estr]} {down}{d};
	#replace {format_exits_inner[estr]} {east}{e};
	#replace {format_exits_inner[estr]} {west}{w};
    #replace {format_exits_inner[estr]} {up}{u};
    
    #list result create $format_exits_inner[estr];
    #unvar format_exits_inner;
};

#nop ============ Module private function start ===============================;

#alias {_hnd_roomdesc} {
    #regexp {%1} {^%*%s-%s$} {
        #nop #send {get all};
        #var hnd_addesc_inner[desc_start] 1;
        #var hnd_addesc_inner[desc] {};
        #var map[room][unit] {};
        #var map[room][indoor] 1;
        #var map[room][season] {};
		#var map[room][weather] {};
		#var map[room][item] {};
        #var map[room][name] &1;
    } {
        #if {&hnd_addesc_inner[desc_start] != 0} {
            #regexp {%1} {^%s这里%*{方向|出口}{是|有}%*} {
                #var hnd_addesc_inner[desc_start] 0;
                #var map[room][exit] @format_exits{&5};
            };
            #regexp {%1} {^%s「%*」: %*。$} {
                #var hnd_addesc_inner[desc_start] 0;
                #var map[room][indoor] 0;
				#var map[room][season] &2;
				#var map[room][weather] &3;
            };
            #regexp {%1}{^%s你可以看看(look):%*$}
			{
				#nop #var hnd_addesc_inner[desc_start] 0;
				#var map[room][item] {&2};

				#replace {map[room][item]}{,}{;};
			};
            #if {$hnd_addesc_inner[desc_start] == 1} {
                #var hnd_addesc_inner[desc] @trim{$hnd_addesc_inner[desc]%1};
                #unvar result;
            };
            #regexp {%1} {^%s%S「%S」%S(%w %w)%*}
			{
				#var map[room][unit][&5 &6] &4;
			}{
				#regexp {%1} {^%s%S(%w %w)%*}
				{
					#var map[room][unit][&3 &4] &2;
				}{
					#regexp {%1} {^%s%S%s%S(%w %w)%*}
					{
						#var map[room][unit][&5 &6] &4;
					}{
						#regexp {%1}{^%s%S%s%S%s%S(%w %w)%*}
						{
							#var map[room][unit][&7 &8] &6;
						};
					};
				};
			};
			#nop exclude items in units;
			#unvar map[room][unit][Huo tong];
			#unvar map[room][unit][Yun tie];
			#unvar map[room][unit][Shi zi];
			#unvar map[room][unit][Xiuhua bengjia];
			#unvar map[room][unit][Jin hua];
			
            #regexp {%1} {^>%s} {
                #var map[room][desc] $hnd_addesc_inner[desc];
                #unvar hnd_addesc_inner;
                #showme <139>->_hnd_roomdesc : Get room desc complete<079>;
                #showme ->;
            }; 
        };
    };
};

#nop ============ Module alias start ==========================================;

#alias {on_exit_blocked} {
    #alias cb_on_exit_blocked #cr;
    #alias cb_on_exit_blocked %0;
    
    #act {你的动作还没有完成，不能移动} {
        cb_on_exit_blocked;
    };
    #act {官兵们拦住了你的去路} {
        cb_on_exit_blocked;
    };
};

#alias {map} {
    #switch {"%1"} {
        #case {"load"} {
            #map read data/pku.map;
            #map return;
            map pad;
            dbg map data loaded from data/pku.map;
        };
        #case {"save"} {
            #map write data/pku.map;
            
            dbg map data saved to data/pku.map;
        };
        #case {"reload"} {
            #map destroy;
            map load;
            dbg map data reloaded from data/pku.map;
        };
        #case {"stop"} {
            event_handler_del {_hnd_roomdesc} {RECEIVED LINE};
            #if {$map[status][mapping] == 1} {
                #var map[status][mapping] 0;
                ades stop;
                
                #class map_start_inner kill;
            };
            #class ad_inner kill;
            dbg mapping stopped;
        };
        #case {"pad"} {
            #alias {mappad} {
                #map goto {$map[room][name]}{}{$map[room][desc]}{$map[room][area]};
                show_room_data;
                #unalias mappad;
                dbg map padding complete;
            };
            #if {$map[status][mapping] == 0} {
                get_area {
                    on_get_room {
                        mappad;
                    };
                };
            };
            #elseif {$map[status][mapping] == 1} {
                    mappad;  
            };
            
        };
        #case {"start"} {
            
            
            get_area;
            dbg start mapping;
            
            #var map[status][mapping] 1;
            
            event_handler_add {_hnd_roomdesc} {RECEIVED LINE};
            
            #class map_start_inner open;
            
            #var ades 0;
            
            #act {_hnd_roomdesc : Get room desc complete} {
                #nop get_area;
                #if {$adnew == 1} {
                    #undelay ad_inner_watchdog;
                    
                    #map dig {$chukou} {new};
                    #map move $chukou;
                    #map name $map[room][name];
                    #map set roomarea $map[room][area];
                    #map set roomdesc $map[room][desc];
                    #map set roomdata $map[room][unit];
                    
                    #var timeout 0;
                    #class ad_inner kill;
                };
                #if {$ades == 1} {
                    ades;
                    
                };
                
                show_room_data;
            };
            #alias {ada} {
                #if {"%%1" != ""} {
                    #var map[room][area] %%1;
                    #showme <139>->room area been changed to : %%1<079>;
                } {
                    #showme <139>->current area : $map[room][area]<079>;
                };
            };
            #alias {ades} {
                #if {"%%1" == "start"} {
                    #var ades 1;
                    dbg roomdesc auto-store starting;
                };
                #elseif {"%%1" == "stop"} {
                    #var ades 0;
                    dbg roomdesc auto-store stopped;
                };
                #else {
                    #map name $map[room][name];
                    #map set roomdesc $map[room][desc];
                    #map set roomarea $map[room][area];
                    #map set roomdata $map[room][unit];
                    
                    #nop #map get;
                    dbg room data updated : area $map[room][area];
                };
            };
            #alias {pre} {
                #if {"%%1" != ""} {
                    
                    
                    #var pre[exit] %%1;
                    #map flag nofollow;
                    #send $pre[exit];
                    
                    #delay {2} {
                        #map find {$map[room][name]}{}{$map[room][desc]};
                        #path save f pre[path];
                        #if {&pre[path] != 0} {
                            #replace pre[path] {\;} { };
                            #list pre[path] create $pre[path];
                            
                            #map get roomvnum pre[lastvnum];
                            
                            #list pre[path] size pre[pathlen];
                            #loop 1 $pre[pathlen] i {
                                #map move $pre[path][$i];
                            };
                            #unvar i;
                            #map get roomvnum pre[targetvnum];
                            #map get roomexits pre[targetexits];
                            #list pre[targetexits] create $pre[targetexits][];
                            #map goto $pre[lastvnum];
                            
                            #if {0 == @isListMember{pre[targetexits];@reverse{$pre[exit]}}} {
                                dbg 出口 $pre[exit] 存在房间记录 $pre[targetvnum];
                            };
                        } {
                            
                            dbg 出口 $pre[exit] 还没有房间记录; 
                        };
                        #send @reverse{$pre[exit]};
                        #map flag nofollow;
                        #unvar pre;
                    };
                    
                    
                } {
                    #showme <129> Usage : pre <direction><079>;
                };
            };
            #alias {ad} {
                #undelay ad_inner_watchdog;
                
                #class ad_inner open;
                
                #var adnew 1;
                #var chukou %%1;
                #var timeout 1;
                
                #delay {ad_inner_watchdog} {
                    #if {$timeout == 1} {
                        #class ad_inner kill;
                        #showme <139>->ad timeout<079>;
                    };
                } {3};
                
                #class ad_inner close;
                
                #send $chukou;
            };
            #class map_start_inner close;
        };
        #default {
            #showme <129>Usage : map <load | save | reload | start | stop><079>;
        };
    };
};

#alias {on_handle_roomdesc} {
    #alias cb_on_handle_roomdesc #cr;
    #alias cb_on_handle_roomdesc %0;
    
    #act {_hnd_roomdesc : Get room desc complete} {
        cb_on_handle_roomdesc;
    };
};
#alias {on_get_room} {
    #class on_get_room_inner open;
    
    #alias cb_get_current_room #cr;
    #alias cb_get_current_room %0;
    
    event_handler_add {_hnd_roomdesc} {RECEIVED LINE};
    on_handle_roomdesc {
        event_handler_del {_hnd_roomdesc} {RECEIVED LINE};
        cb_get_current_room;
        #class on_get_room_inner kill;
    };
    
    #class on_get_room_inner close;
    #send look;
};
#alias {get_area} {
    #class get_area_inner1 open;
    
    
    #alias {get_area_end} {
        #class get_area_inner1 kill;
        #delay {dl_get_area} {
            get_area_map_end;
        } {2};    
    };
    #act {这里暂时没有地图} {
        #class get_area_inner1 kill;
        #class get_area_inner2 kill;
        dbg 当前房间没有地图信息，请走几步再试
    };
    #act {^【%*地图】} {
        #var map[room][area] %%1;
        get_area_end;
    };
    #act {^【%*地图－%*】} {
        #var map[room][area] %%1%%2;
        get_area_end;
    };
    #class get_area_inner1 close;
    
    #class get_area_inner2 open;
    #alias cb_get_area #cr;
    #alias cb_get_area %0;
    #alias {get_area_map_end} {
        cb_get_area;
        #class get_area_inner2 kill;
        dbg area changed to : $map[room][area];
    };
    #act {== 未完继续} {
        #send q;
    };
    #act {[临时存储讯息]} {
        #undelay {dl_get_area};
        get_area_map_end;
    };
    #class get_area_inner2 close;
    #send localmaps;
};
#alias {show_room_data} {
    #map get roomexits tmp_room_data[knownexit];
    #list tmp_room_data[knownexit] create $tmp_room_data[knownexit][];
    
    #foreach {$map[room][exit][%*]} {eett} {
        #list tmp_room_data[knownexit] find {$eett} idx;
        #if {$idx == 0} {
            #list tmp_room_data[unknownexit] add $eett;
        }; 
    };

    #showme ->;
    #showme <139>->房间出口:$map[room][exit]<079>;
    #showme <119>->未知出口:$tmp_room_data[unknownexit]<079>;
    #showme <129>->已知出口:$tmp_room_data[knownexit]<079>;
    #map get roomarea tmp_room_data[area];
    #map get roomname tmp_room_data[name];
    #map get roomdesc tmp_room_data[desc];
    #showme <129>->已知描述:$tmp_room_data[area] => $tmp_room_data[name] => $tmp_room_data[desc]<079>;
    #nop #showme ->$map[room][area] => ${map[room][name]} => ${map[room][desc]};
    #showme ->;
    
    #unvar tmp_room_data;#unvar eett;#unvar idx;
};

#alias {carto} {
    #if {"%2" == ""} {
        #map dig to%1;
    } {
        #map link to%1 %2;
    };
    #map exit to%1 COMMAND {qu %1};
};

#alias {pathwalk} {
    #class pathwalk_inner open;
    
    #act {^%S%s-%s$} {
        #if {"%%1" !== "马车"} {
            #path walk;
        };
    };
    
    #class pathwalk_inner close;
    
    #path walk;
};

#nop 

#alias {gt} {
    #if {"%1" == "stop"} {
        set brief 0;
        #class gt_inner kill;
    };
    #elseif {"%1" != ""} {
        
        #class gt_inner open;
        
        #var gt_start 0;
        #var gt_car 0;
        #var gt_boat 0;
        #var gt_walkend 0;
        
        #act {#SHORTEST PATH: NO PATH FOUND TO} {
            #var gt_start 0;
            set brief 0;
            
            #showme <119>找不到目的地：%1<079>
            #class gt_inner kill;
        };
        #act {这个方向没有出路} {
            dbg 行走出错;
            gt stop;
            #buffer lock;
        };
        #act {接过你递给的船资} {
            #var gt_start 0;
        };

        #act {王兴隆说道：“后面是我家，没事别瞎转悠} {
            #var gt_start 0;
            hit wang;
        };
        #act {王兴隆脚下一个不稳，跌在地上一动也不动了} {
            #var gt_start 1;
            
        };
        #act {王兴隆死了} {
            #var gt_start 1;
        };
        #act {童百熊说道：「你不是我日月神教弟子，来我教干什么？速速离开！」} {
            #var gt_start 0;
            hit tong;
        };
        
        #act {童百熊脚下一个不稳，跌在地上一动也不动了} {
            #var gt_start 1;
        };
        
        #act {童百熊死了} {
            #var gt_start 1;
        };
        
        #act {陆大有喝道：你不是华山弟子，不得入内} {
            #var gt_start 0;
            hit lu;
        };
        
        #act {陆大有脚下一个不稳，跌在地上一动也不动了} {
            #var gt_start 1;
        };
        
        #act {陆大有死了} {
            #var gt_start 1;
        };

        #act {你登上了一辆马车} {
            #var gt_car 1;
        };
        
        #act {在出发前，你结清了车资} {
            #var gt_start 0;
        };
        #act {你身上没有车资%*，车夫拒绝开车} {
            gt stop;
        };
        #act {马车缓缓开动} {
            #var gt_start 0;
        };
        
        #act {你从马车上走了下来} {
            #var gt_car 0;
            #var gt_start 1;
        };
        
        #act {你可以下车(xia)了} {
            xia;
            #var gt_car 0;
            
            #if {$gt_walkend == 1} {
                #class gt_inner kill;
            } {
                #var gt_start 1;
            };
        };
        
        #act {你从踏板上走上了船} {
            #var gt_start 0;
            #var gt_boat 1;
        };
        
        #act {船夫把踏脚板收起来，说了一声“坐稳喽”，船桨一点，小船向东海驶去} {

        };
        
        #act {船夫对你说道：“到了，上岸吧。”，随即把一块踏脚板搭上堤岸} {
            #var gt_start 1;
            #var gt_boat 0;
        };
        
        #act {你沿着踏板走了上去} {
            #var gt_start 1;
            #var gt_boat 0;
        };
        
        #act {你吸了口气，一声“船家”，声音中正平和地远远传了出去} {
            #var gt_start 0;
            #var gt_boat 1;
        };
        
        #act {艄公把踏脚板收起来，说了一声“坐稳喽”} {

        };
        
        #act {船夫把踏脚板收起来，说了一声“坐稳喽”，船桨一点，小船向湖心驶去} {

        };
        
        #act {艄公要继续做生意了，所有人被赶下了渡船} {
            #var gt_start 1;
        };
        #act {船夫对你说道：“到了，上岸吧。”，随即把一块踏脚板搭上堤岸} {
            #var gt_start 1;
            #var gt_boat 0;
            #showme <139>->下船了<079>;
        };
        
        #act {艄公说“到啦，上岸吧”，随即把一块踏脚板搭上堤岸} {
            out;
            #var gt_boat 0;
            
            #if {$gt_walkend == 1} {
                #class gt_inner kill;
            } {
                #var gt_start 1;
            };
        };
        
        #act {绿衣少女将小船系在树枝之上，你跨上岸去} {
            #var gt_start 1;
        };
        
        #act {你跃上小舟，船就划了起来} {
            #var gt_start 0;
        };
        
        #act {小舟终于划到近岸，你从船上走了出来} {
            #var gt_start 1;
        };
        
        #act {你拿起船桨用力划了起来} {
            #var gt_start 0;
        };
        #act {不知过了多久，船终于靠岸了，你累得满头大汗} {
            #var gt_start 1;
            #var gt_boat 0;
        };
        #act {你跃上小舟，小船晃了晃，你吓得脸色苍白} {
            
        };
        
        #ticker {walk_path} {
            #if {$gt_start == 1} {
                #path walk;
            };
            #if {$gt_walkend == 1 && $gt_car == 0 && $gt_boat == 0} {
                #showme <129>到达目的地：%1<079>;
                #buffer lock;
                #class gt_inner kill;
            };
        } {0.5};
        
        #event {END OF PATH} {
            #var gt_walkend 1;
            #var gt_start 0;
            set brief 0;
        };
        
        #class gt_inner close;
        
        #map find {%1} {%2} {%3} {%4} {%5};
        set brief 1;
        #var gt_start 1;
    };
    #else {
        #showme Usage : gt <目的地名>;
    };
};

#alias {driveto} {
    #class driveto_inner open;
    
    #act {你可以下车(xia)了} {
        xia;
        #class driveto_inner kill;
    };
    
    #class driveto_inner close;
    gu;qu %1;
};

#alias {${mod_name}_guojiang} {
    #class guojiang_inner open;
    
    #act {艄公说“到啦，上岸吧”，随即把一块踏脚板搭上堤岸} {
        out;
        #class guojiang_inner kill;
    };
    
    #class guojiang_inner close;
    
    yell boat;enter;
};

#class $mod_name close;

${mod_name}_load;
