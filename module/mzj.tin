#class $mod_name open;

#alias {${mod_name}_load} {
    #showme Module $mod_name loaded;
    #unalias ${mod_name}_load;
};

#alias {${mod_name}_unload}{
    #showme Module $mod_name unloaded;
    #class $mod_name kill;
};

#alias {${mod_name}_help}{
    #showme <139>模块帮助文档 : ${mod_name}<079>;
    
    /* TODO: place your HELP doc here */
    
    #showme  ;
    #showme <139> -> 按任意键继续...<079>;
    #buffer lock;
};

#act {没有经过通传，任何人等不得擅闯都统治府} {
    ask shiwei about 通传;
    do_rewalk;
};

#var mzj[status_id] {};
#var mzj[status_id][none]     空闲状态;
#var mzj[status_id][askjob]   申请任务;
#var mzj[status_id][waitjob]  等待任务;
#var mzj[status_id][working]  执行任务;
#var mzj[status_id][complete] 任务完成;

#var mzj[status] none;
#var mzj[auto] 0;

#alias {mzj.start} {
    #show <139>mzj: 开始刺杀任务<079>;
    mzj_status_set askjob;
};
#alias {mzj.stop} {
    #show <139>mzj: 停止刺杀任务<079>;
    mzj_status_set none;
};
#alias {mzj.auto} {
    #if {$mzj[auto] == 1} {
        #show <139>mzj: 关闭自动模式<079>;
        #var mzj[auto] 0;
    } {
        #show <139>mzj: 打开自动模式<079>;
        #var mzj[auto] 1;
    };
};
#alias {mzj_decode} {
    #unvar mzj[pass_table];
    #act {^{([0-9]{1,2})[ ]?([^ ]+)}} {
        #var mzj[pass_table][%%2] {@split_string_zh{%%3}};
    };
    #act {^} {
        #unact {^};
        #act {^$} {
            #unact {^$};
            #unact {^{([0-9]{1,2})[ ]?([^ ]+)}};
            
            #var mzj[pos] {};
            
            #loop {1}{4}{myidx}{
                #if {&mzj[pass][$myidx] != 0} {
                    #var mzj[pos] {$mzj[pos]${mzj[pass_table]$mzj[pass][$myidx]}};
                } {
                    #break;
                };
            };
            #unvar myidx;
            
            #system {echo -e "$mzj[pos]" >> data/mzj.log};
            log mzj 译码: $mzj[pos];
            #show <139>mzj:解密完成<079>;
        };    
    };
    
    duizhao;
};

#alias {mzj_status_set} {

    #if {"%1" == "" || "%1" == "$mzj[status]"} {
        #nop;
    } {
        #if {&mzj[status_id][%1] == 0} {
            log mzj 无效状态 %1;
        } {
            #if {"$mzj[status]" != "none"} {
                #class mzj_status_$mzj[status]_inner kill;
                mzj_status_exit_$mzj[status];
            };
            
            #var mzj[status] {%1};
            
            log mzj $mzj[status_id][$mzj[status]];
            
            #if {"$mzj[status]" != "none"} {
                mzj_status_enter_$mzj[status];
            };
        };
    };
    #nop;
};

#alias {mzj_status_exit_askjob} {
    #undelay {dl_askjob};
    #unvar mzj[askline];
    #unvar mzj[buffer];
    #unvar mzj[owner];
};
#alias {mzj_status_enter_askjob} {
    #class mzj_status_askjob_inner open;
    
    #act {孟之经说道：「这里人多眼杂，你先到%*等候，我自会通知你。」} {
        #var mzj[waitpos] %%1;
        
        #buffer get mzj[askline] 1;
        #script {mzj[buffer]}{echo -e "$mzj[askline]"|sed -r "s/\x1B\[([0-9]{1,3}((;[0-9]{1,3})*)?)?[m|K]//g"};
        #regexp {$mzj[buffer][1]} {^%*向孟之经打听有关%*{job|情报}%*的消息。} {
            #var mzj[owner] &1;
        } {
            #var mzj[owner] no_one;
        };
        
        log mzj 接头地点：$mzj[waitpos];
        
        #if {"$mzj[owner]" == "你"} {
            mzj_status_set waitjob;
        };
    };
    #act {^孟之经说道：「你上次大发神威之后，汉奸们都大多不敢出头了，过段时间再来吧。[%d:%d]」} {
        log mzj 需要等待: %%1:%%2;
        #math mzj[waitime] {%%1 * 60 + %%2};
        
        #delay {dl_askjob} {
            ask meng about job;
        }{$mzj[waitime]};
    };
    
    #class mzj_status_askjob_inner close;
    
    #delay {dl_askjob} {
        ask meng about job;
    }{1};
};

#alias {mzj_status_exit_waitjob} {
    #undelay {dl_waitjob};
};
#alias {mzj_status_enter_waitjob} {
    #class mzj_status_waitjob_inner open;
    
    #alias {mzjpass} {
        #unvar mzj[pass];
        
        #if {"%%2" != ""} {
            #var mzj[pass][1] [%%1][%%2];
        };
        #if {"%%4" != ""} {
            #var mzj[pass][2] [%%3][%%4];
        };
        #if {"%%6" != ""} {
            #var mzj[pass][3] [%%5][%%6];
        };
        #if {"%%8" != ""} {
            #var mzj[pass][4] [%%7][%%8];
        };
        #if {"%%10" != ""} {
            #var mzj[pass][5] [%%9][%%10];
        };
        mzj_decode;
    };
    
    #act {^孟之经托付都府内常随送给了你一页密码} {
        log mzj 收到任务密码;
        
        #act {http://pkuxkx.{com|net}/antirobot/robot.php?filename=%*} {
            log mzj 图片任务;
            #unact {http://pkuxkx.{com|net}/antirobot/robot.php?filename=%*};

            fetch_captcha %%%2;
        };
    };
    #act {^mzj:解密完成} {
        mzj_status_set working;
    };
    #act {^孟之经(meng zhijing)告诉你：第%*个字在：第%*行，第%*列。第%*个字在：第%*行，第%*列。对照(duizhao)这页} {
        #unact {http://pkuxkx.{com|net}/antirobot/robot.php?filename=%*};
        #unvar mzj[pass];
        
        #var mzj[pass][@ctd{%%1}] {[@ctd{%%2}][@ctd{%%3}]};
        #var mzj[pass][@ctd{%%4}] {[@ctd{%%5}][@ctd{%%6}]};
        
        mzj_decode;
    };
    #act {^孟之经(meng zhijing)告诉你：第%*个字在：第%*行，第%*列。第%*个字在：第%*行，第%*列。第%*个字在：第%*行，第%*列。对照(duizhao)这页} {
        #unact {http://pkuxkx.{com|net}/antirobot/robot.php?filename=%*};
        #unvar mzj[pass];
        
        #var mzj[pass][@ctd{%%1}] {[@ctd{%%2}][@ctd{%%3}]};
        #var mzj[pass][@ctd{%%4}] {[@ctd{%%5}][@ctd{%%6}]};
        #var mzj[pass][@ctd{%%7}] {[@ctd{%%8}][@ctd{%%9}]};
        
        mzj_decode;
    };
    #act {^孟之经(meng zhijing)告诉你：第%*个字在：第%*行，第%*列。第%*个字在：第%*行，第%*列。第%*个字在：第%*行，第%*列。第%*个字在：第%*行，第%*列。对照(duizhao)这页} {
        #unact {http://pkuxkx.{com|net}/antirobot/robot.php?filename=%*};
        #unvar mzj[pass];
        
        #var mzj[pass][@ctd{%%1}] {[@ctd{%%2}][@ctd{%%3}]};
        #var mzj[pass][@ctd{%%4}] {[@ctd{%%5}][@ctd{%%6}]};
        #var mzj[pass][@ctd{%%7}] {[@ctd{%%8}][@ctd{%%9}]};
        #var mzj[pass][@ctd{%%10}] {[@ctd{%%11}][@ctd{%%12}]};
        
        mzj_decode;
    };
    
    #class mzj_status_waitjob_inner close;
    
    #delay {dl_waitjob} {
        gt $mzj[waitpos];
        on_there {
            log mzj 到达接头地点;
        };
    }{1};
};

#alias {mzj_status_exit_working} {
    #undelay {dl_working};
};
#alias {mzj_status_enter_working} {
    #class mzj_status_working_inner open;
    
    #act {^对方不愿和你较量} {
        bfs 15;
    };
    #act {^你定睛一看，%*正是你要找的汉奸卖国贼！} {
        log mzj 看见目标: %%1;
        #var mzj[target][name] {%%1};
        
        #class mzj_status_working_inner open;
        
        #act {^$mzj[target][name]往%*落荒而逃了}{
            #map move $direction[cn][%%%1];
        };
        #act {^这里不准战斗} {
            ask $mzj[target][id] about fight;
        };
        #act {^$mzj[target][name]往%*离开} {
            #map move $direction[cn][%%%1];
        };
        #act {^    大元 %S $mzj[target][name](%w %w)} {
            #var mzj[target][id] {@lower{%%%2} %%%3};
            
            log mzj 目标ID: $mzj[target][id];
            
            trvs.stop;
            follow $mzj[target][id];
            kill $mzj[target][id];
        };
        
        #class mzj_status_working_inner close;
        
        trvs.stop;
        gts;
    };
    #act {^恭喜！你完成了都统制府行刺任务} {
        mzj_status_set complete;
    };
    #act {^遍历路径:} {
        #nop trvs.start;
        _path_run trvs;
    };
    
    #class mzj_status_working_inner close;
    
    #unvar mzj[parsed_pos];
    parse_addr $mzj[pos] mzj[parsed_pos];
    
    #delay {dl_working} {
        gt %*$mzj[parsed_pos][name]%* $mzj[parsed_pos][area];
        on_there {
            bfs 15;
        };
    }{1};
};

#alias {mzj_status_exit_complete} {
    #undelay {dl_complete};
};
#alias {mzj_status_enter_complete} {
    #class mzj_status_complete_inner open;
    
    #var itemid {};
    #var itemid {{石炭}{shi tan}{玄冰}{xuan bing}};
    
    #act {^完成了都统制府刺杀任务后，你被奖励了：} {
        
    };
    #act {^你获得了%*份{石炭|玄冰}【{劣质|瑕疵}】} {
        drop $itemid[%%2];
    };
    #act {^你累计共完成了%d次都统制府刺杀任务} {
        log mzj 刺杀任务计数: %%1 次;
        
        #delay {dl_complete} {
            #if {$mzj[auto] == 1} {
                mzj_status_set askjob;
            } {
                mzj_status_set none;
            };
        }{1};
    };
    #class mzj_status_complete_inner open;
    
    #delay {dl_complete} {
        gt 孟之经;
        on_there {
            ask meng about finish;
        };
    }{1};
};

#class $mod_name close;

${mod_name}_load;
