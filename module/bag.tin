#class $mod_name open;

#alias {${mod_name}_load} {
    #showme Module $mod_name loaded;
    #unalias ${mod_name}_load;
};

#alias {${mod_name}_unload}{
    #showme Module $mod_name unloaded;
    #class $mod_name kill;
};

#var bag {};
#list bag[sell_list] add {Heise pifeng;Changjian;Tie bishou;Chang bian};

#alias {_count} {
    #regexp {%2} {%*{两|文|粒|块|件|柄|把}%*} {
        #var bag[items][%1] @ctd{&1};
    };
};            
#alias {_hnd_i2} {
    
    #if {$_hnd_i2_start == 1} {
        #regexp {%1} {%*(%*)} {
            #var bag[items][&2] {1};
            _count {&2} {&1};
            #if { 1 == @isListMember{bag[sell_list];&2}} {
                #list bag[forsell] add {&2};
            };
        };
    };
    #regexp {%1} {你身上带著下列这些东西(负重%s%d\%)} {
        #var _hnd_i2_start 1;
        #var bag[weight] &2;
    };
    #regexp {%1} {^>%s} {
        #unvar _hnd_i2_start;
        #tic {tk_sell} {
            #list bag[forsell] size _len;
            #if {$_len !=0 } {
                #list bag[forsell] get {1} {_name};
                #list bag[forsell] delete 1;
                #send {sell @lower{$_name} for $bag[items][$_name]};
            } {
                #untic tk_sell;
            };
        } {1};
    };
};

#nop =========================================================================;

#alias {gc} {
    #if {"%1" == ""} {
        get all from corpse;    
    } {
        get all from corpse %1;
    };
    drop jin hua;
    drop ling;
};

#alias {pgem} {
    #class pgem_inner open;
    #act {你要把什么东西放进宝石袋} {
        #class pgem_inner kill;
    };
    #act {你将手中%*放进了宝石袋} {
        pack gem;
    };
    #class pgem_inner close;
    pack gem;
};
#alias {mgem} {
    #if {"%1" != ""} {
        do 3 pickgem %1 ;combine gem;pack gem;
    } {
        #showme <129> Usage : mgem <gem id><079>;
    };
};

#alias {sellall} {
    event_handler_add {_hnd_i2} {RECEIVED LINE};
    #unvar bag;
    #send i2;
    
    #delay {3}{event_handler_del {_hnd_i2} {RECEIVED LINE};};
};

#class $mod_name close;

${mod_name}_load;
