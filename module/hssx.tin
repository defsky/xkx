#class $mod_name open;

#alias {${mod_name}_load} {
    #showme Module $mod_name loaded;
    #unalias ${mod_name}_load;
};

#alias {${mod_name}_unload}{
    #unvar hssx;
    #showme Module $mod_name unloaded;
    #class $mod_name kill;
};

#alias {${mod_name}_help}{
    #showme <129>Module Help : ${mod_name}<079>;
    #showme <129> job 获取任务<079>;
    #showme <129> fin 完成任务<079>;
    #showme <129> fai 失败任务<079>;
    #showme <129> sx  送信<079>;
};

#alias {job} {ask yue about job};
#alias {fin} {ask yue about finish};
#alias {fai} {ask yue about fail};

#alias {_sxmsg} {
    #showme <139> hssx : %0<079>;
};

#nop 你从怀中掏出信交给荆猛波，道：「这是岳不群先生托在下送给您的信，请收好。」;
#nop 荆猛波拆开信浏览了一遍，对你笑道：「多谢多谢，辛苦你了。」;
#nop 你的任务完成，快回去复命吧。;

#var task {{target}{}{pos}{}{ok}{0}{fight}{0}{rob}{}{robcount}{0}{id}{}};

#alias {check_rob} {

    #if {"%1" == "on"} {
        #class on_robber_found_inner open;
        
        #var hssx[robcount] 0;
        #var hssx[roblist] {};
        
        #act {你伸手向怀中一摸，发现密函已经不翼而飞} {
            #unact {你伸手向怀中一摸，发现密函已经不翼而飞};
            goto_stop;
            #send look;
            _sxmsg 密函被偷了！！！;
        };
        
        #list on_robber_found_trigger_list create {
            ^%S说道：「既然甘当岳不群那老贼的走狗，就别怪本大爷不客气了！」;
            ^%S笑道：「%S你别逞能，点子爪子硬，老子来帮你！」;
            ^%S说道：「嘿嘿，让本大爷来教训教训你！」;
            ^%S说道：「想跑，没那么容易！」;
        };
        
        #foreach {$on_robber_found_trigger_list[%*]} {regstring} {
            #act {$regstring} {
                #if {&hssx[roblist][%%1] == 0} {
                    #var hssx[robname] %%1;
                    #var hssx[roblist][%%1] {};
                    #var hssx[robdown] false;
                    #math hssx[robcount] {$hssx[robcount] + 1};
                    
                    #class hssx_rob_list open;
                    
                    #act {$hssx[robname](%w %w)} {
                        _sxmsg @lower{%%%1 %%%2};
                        kill @lower{%%%1 %%%2};
                    };
                    #act {^$hssx[robname]死了} {
                        dbg $hssx[robname] died;
                        #math hssx[robcount] {$hssx[robcount] - 1};
                        
                        #if {$hssx[robcount] <= 0} {
                            #class hssx_rob_list kill;
                            #var hssx[robdown] true;
                            
                            _sxmsg 劫匪处理完毕，可以送信了;
                            log 抢信劫匪处理完毕;
                            #delay {10} {look};
                        };
                        #delay {hssx_gc}{gc}{4};
                        #delay {hssx_gc2}{gc 2}{8};
                    };
                    
                    #class hssx_rob_list close;
                    
                    #unvar hssx[robname];
                    
                    #delay {hssx_look}{look}{2};
                };
            };
        };
        
        #class on_robber_found_inner close;
    };
    #elseif {"%1" == "off"} {
        #class on_robber_found_inner kill;
    };
    
};

#alias {on_get_receiver} {
    #class on_get_receiver_inner open;
    
    #alias cb_on_get_receiver #cr;
    #alias cb_on_get_receiver %0;
    
    #act {^           收信人：%S(%w %w)} {
        #var hssx[id] {%%2 %%3};
        #var hssx[name] %%1;
        cb_on_get_receiver;
        #class on_get_receiver_inner kill;
    };
    #act {^           收信人：%S(%S)} {
        #var hssx[id] %%2;
        #var hssx[name] %%1;
        cb_on_get_receiver;
        #class on_get_receiver_inner kill;
    };
    
    #class on_get_receiver_inner close;
    
    look mi han;
};

#alias {check_receiver} {
    #act {$hssx[name]($hssx[id])} {
        #if {"$hssx[robdown]" == "true"} {
            #act {^你的任务完成，快回去复命吧} {
                log 送信任务完成;
                #unact {^你的任务完成，快回去复命吧};
                check_rob off;
                #var hssx[ok] 1;
                
                #delay {1.5} {
                    on_map_pad {
                        on_there {
                            fin;
                            #delay {0.5} {#buffer lock;};
                        };
                        goto 书房 华山;
                    };
                };
                
            };
            #unact {$hssx[name]($hssx[id])};
            sx;
        } {
            _sxmsg 找到收信人，先杀劫匪再送信：sx;
            
            _sxmsg -----------------;
        };
    };
};

#alias {parse_task} {
    #var hssx[robdown] false;
    check_rob on;
    #class temp_inner kill;
    
    #var hssx[target] %2;
    #var hssx[pos] %1;
    #var hssx[ok] 0;
    #var hssx[fight] 0;
    #var hssx[robcount] 0;
    #var hssx[rob] {};
    
    on_get_receiver {
        check_receiver;
        #delay {1} {
            _sxmsg $hssx[pos];
            gotowork $hssx[pos]
        };
    };
    
    pu;
    
    #act {^浩气苍冥功运功完毕，心中壮怀之激烈情绪犹留心中} {
        yun powerup;
    };
};
#act {你向岳不群打听有关『job』的消息。} {
    #class temp_inner open;
    
    #act {^岳不群说道：「%*下去休息休息吧。」} {
        #class temp_inner kill;
    };
    #act {^岳不群道：「多谢壮士，你赶紧把它送到%*附近的%*手中。」} {
        parse_task %%1 %%2;
        #system {echo "%%1">>data/hssx.log};
    };
    #act {^岳不群道：「多谢小兄弟，你赶紧把它送到%*附近的%*手中。」} {
        parse_task %%1 %%2;
        #system {echo "%%1">>data/hssx.log};
    };
    #act {^岳不群对你道：「我这里正好有封密函，麻烦你跑一趟，交给%*附近的%*。」} {
        parse_task %%1 %%2;
        #system {echo "%%1">>data/hssx.log};
    };
    #class temp_inner close;
};
#nop    「六合连环红」戴小八(Dai xiaoba);
#nop    「南海迅雷棒」冯捷梅(Feng jiemei) <战斗中>;

#alias {sx} {
    songxin @lower{$hssx[id]};
};

#class $mod_name close;

${mod_name}_load;
