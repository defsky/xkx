#class $mod_name open;

#alias {${mod_name}_load} {
    #showme Module $mod_name loaded;
    #unalias ${mod_name}_load;
};

#alias {${mod_name}_unload}{
    #showme Module $mod_name unloaded;
    #class $mod_name kill;
};

#alias {${mod_name}_help}{
    #showme <129>Module Help : ${mod_name}<079>;
    #showme <129> job 获取任务<079>;
    #showme <129> fin 完成任务<079>;
    #showme <129> fai 失败任务<079>;
    #showme <129> sx  送信<079>;
};

#alias {job} {ask yue about job};
#alias {fin} {ask yue about finish};
#alias {fai} {ask yue about fail};

#nop 你从怀中掏出信交给荆猛波，道：「这是岳不群先生托在下送给您的信，请收好。」;
#nop 荆猛波拆开信浏览了一遍，对你笑道：「多谢多谢，辛苦你了。」;
#nop 你的任务完成，快回去复命吧。;

#var task {{target}{}{pos}{}{ok}{0}{fight}{0}{rob}{}{robcount}{0}{id}{}};

#alias {parse_task} {
    #class temp_inner kill;
    
    #var task[target] %2;
    #var task[pos] %1;
    #var task[ok] 0;
    #var task[fight] 0;
    #var task[robcount] 0;
    #var task[rob] {};
    
    #act {^           收信人：%S(%w %w)} {
        #var task[id] @lower{%%2 %%3};
    };
    #act {^           收信人：%S(%w)} {
        #var task[id] @lower{%%2};
    };
    #send {look mi han};
    pu;
    
    #act {^浩气苍冥功运功完毕，心中壮怀之激烈情绪犹留心中} {
        yun powerup;
    };
    #act {你伸手向怀中一摸，发现密函已经不翼而飞} {
        gt stop;
        goto_stop;
        #send look;
        #var task[fight] 1;
        dbg 密函被偷了！！！;
    };
    #act {你，把密函给我乖乖交出来吧} {
        gt stop;
        goto_stop;
        #send look;
        #var task[fight] 1;
        dbg 抢信的来了！！！;
    };
    #act {你的任务完成，快回去复命吧} {
        #var task[ok] 1;
        #delay {0.5} {#buffer lock;};
    };
    #act {%*死了。} {
        #if {$task[fight] == 1} {
            #delay {1} {get all from corpse;};
            
            #if {1 == @isListMember{task[rob];%%1}} {
                #math task[robcount] {$task[robcount] + 1};
            };
            #list task[rob] find %%1 idx;
            #if {$idx != 0 } {
                #list task[rob] delete $idx;
            };
            #list task[rob] size len;
            #if {$len == 0} {
                #var task[fight] 2;
            };
            #unvar idx;#unvar len;
        };
    };
};
#act {你向岳不群打听有关『job』的消息。} {
    #class temp_inner open;
    
    #act {^岳不群道：「多谢壮士，你赶紧把它送到%*附近的%*中。」} {
        parse_task %%1 %%2;
        #system {echo "%%1">>data/hssx.log};
    };
    #act {^岳不群道：「多谢小兄弟，你赶紧把它送到%*附近的%*手中。」} {
        parse_task %%1 %%2;
        #system {echo "%%1">>data/hssx.log};
    };
    #act {^岳不群对你道：「我这里正好有封密函，麻烦你跑一趟，交给%*附近的%*。」} {
        parse_task %%1 %%2;
        #system {echo "%%1">>data/hssx.log};
    };
    #class temp_inner close;
};
#nop    「六合连环红」戴小八(Dai xiaoba);
#nop    「南海迅雷棒」冯捷梅(Feng jiemei) <战斗中>;

#act {看起来%*想杀死你} {
    dbg %1;
    
};
#act {^    %S%s%S(%w %w)$} {
    #var npc_name {%4 %5};
    
    #if {$task[fight] == 1} {
        #regexp {%0} {^    「%S」%S(%w %w)} {
            #list task[rob] add &2;
            dbg 劫匪ID : &2 &3 &4;
        };
        
    };
    #elseif {"@lower{$npc_name}" == "$task[id]" && $task[ok] == 0} {
        #if {$task[fight] == 2} {
            sx;
        } {
            dbg 找到收信人，先杀劫匪再送信：sx;
        };
    };
};
#alias {sx} {
    songxin $task[id];
};

#class $mod_name close;

${mod_name}_load;
