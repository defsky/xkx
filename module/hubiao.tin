#class $mod_name open;

#alias {${mod_name}_load} {
    #showme Module $mod_name loaded;
    #unalias ${mod_name}_load;
};

#alias {${mod_name}_unload}{
    #showme Module $mod_name unloaded;
    #class $mod_name kill;
};

#alias {${mod_name}_help}{
    #showme <139>模块帮助文档 : ${mod_name}<079>;
    
    
    #showme  ;
    #showme <139> -> 按任意键继续...<079>;
    #buffer lock;
};

#read data/hubiao_cache_$status[id];

#var hubiao[status] none;
#var hubiao[luanru] 0;
#var hubiao[mixin][start] 0;

#if {&hubiao[max_lunci] == 0} {
    #var hubiao[max_lunci] 6;
};

#var hubiao[status_id][none]     空闲状态;
#var hubiao[status_id][listesc]  查看任务;
#var hubiao[status_id][getesc]   领取任务;
#var hubiao[status_id][zoubiao]  赶车走镖;
#var hubiao[status_id][robber]   出现劫匪;
#var hubiao[status_id][arrived]  到达地点;
#var hubiao[status_id][complete] 护镖完成;
#var hubiao[status_id][mixin]    密信任务;
#var hubiao[status_id][mixinok]  密信完成;
#var hubiao[status_id][luanru]   镖车乱入;
#var hubiao[status_id][recover]  恢复气血;
#var hubiao[status_id][manual]   手动操作;

#if {&hubiao[lunci] == 0} {
    #var hubiao[lunci] 0;
};

#alias {gn} {gan che to north};
#alias {gs} {gan che to south};
#alias {ge} {gan che to east};
#alias {gw} {gan che to west};
#alias {gne} {gan che to northeast};
#alias {gnw} {gan che to northwest};
#alias {gse} {gan che to southeast};
#alias {gsw} {gan che to southwest};
#alias {gnu} {gan che to northup};
#alias {gnd} {gan che to northdown};
#alias {gsu} {gan che to southup};
#alias {gsd} {gan che to southdown};
#alias {gwu} {gan che to westup};
#alias {gwd} {gan che to westdown};
#alias {geu} {gan che to eastup};
#alias {ged} {gan che to eastdown};
#alias {gup} {gan che to up};
#alias {gdown} {gan che to down};
#alias {genter} {gan che to enter};
#alias {gout} {gan che to out};

#alias {hb.start} {
    hb_status_set listesc;
};
#alias {hb.stop} {
    hb_status_set none;
};
#alias {hb.restart} {
    hb_status_set zoubiao;
};
#alias {hb.max} {
    #var hubiao[max_lunci] %1;
};

#alias {hb_status_set} {

    #if {"%1" == "" || "%1" == "$hubiao[status]"} {
        #nop;
    } {
        #if {&hubiao[status_id][%1] == 0} {
            log hubiao 无效状态 %1;
        } {
            #if {"$hubiao[status]" != "none"} {
                #nop log hubiao 退出状态 $hubiao[status_id][$hubiao[status]];
                #class hb_status_$hubiao[status]_inner kill;
                hb_status_exit_$hubiao[status];
            };
            
            #var hubiao[status] {%1};
            
            log hubiao 进入状态 $hubiao[status_id][$hubiao[status]];
            
            #if {"$hubiao[status]" != "none"} {
                hb_status_enter_$hubiao[status];
            };
        };
    };
    
    #nop;
};

#alias {hb_status_exit_listesc} {
    #undelay {dl_hubiao_listesc};
};
#alias {hb_status_enter_listesc} {
    #class hb_status_listesc_inner open;
    
    #var listesc_start 0;
    #var valid_quest {};
    #list dest_list create {};
    
    #alias {list_esc} {
        #var listesc_start 0;
        #var valid_quest {};
        
        listesc;
    };
    #alias {select_quest} {
        #var hubiao[quest] {};
        #var selected_quest {{id}{}{priority}{999}};
        
        #foreach {$valid_quest[]}{selected_quest[tmp_id]}{
            #if {$dest_list[$valid_quest[$selected_quest[tmp_id]]] < $selected_quest[priority]} {
                #var selected_quest[priority] $dest_list[$valid_quest[$selected_quest[tmp_id]]];
                #var selected_quest[id] $selected_quest[tmp_id];
            };
        };
        
        #var hubiao[quest][id] $selected_quest[id];
        #var hubiao[quest][boss] $valid_quest[$selected_quest[id]];
        
        #unvar selected_quest;
    };
    
    #act {^%*押镖任务列表：} {
        #var listesc_start 1;
    };
    #act {^{(\d+)\s+(\S+)\s+\d+}秒   %S %S} {
        #nop #show <139>%%2-%%3-%%4<079>;
        
        #if {$listesc_start == 1} {
            #if {&dest_list[%%3] == 0} {
                #var dest_list[%%3] 999;
                #var shell_cmd {echo "#var dest_list {$dest_list}">data/hubiao_dest_list.tin};
                #system $shell_cmd;
            };

            #if {"%%4" == "待认领"} {
                #var valid_quest[%%2] %%3;
            };
        };
    };
    #act {^使用命令【getesc 任务序号】来认领押镖任务} {
        #var listesc_start 0;
        
        #if {"$valid_quest" == ""} {
            log hubiao 没有待认领的任务,20秒后再查看;
            #delay {dl_hubiao_listesc} {list_esc} {20};
        } {
            select_quest;
        
            #if {"$hubiao[quest][id]" == ""} {
                log hubiao 未选中任务，10秒后重新选择;
                #delay {dl_hubiao_listesc} {list_esc} {10};
            } {
                hb_status_set getesc;
            };
        };
    };
    
    #class hb_status_listesc_inner close;
    
    #read data/hubiao_dest_list.tin;
    list_esc;
};

#alias {hb_status_exit_getesc} {
    #undelay {dl_getesc};
};
#alias {hb_status_enter_getesc} {
    #class hb_status_getesc_inner open;
    
    #alias {hbpos} {
        #show $status[name]把这批红货送到$hubiao[quest][boss]那里，他已经派了个伙计名叫$hubiao[dest][huoji]到%%1附近接你，把镖车送到他那里就行了。;
    };
    
    #act {^$status[name]把这批红货送到{.*}那里，他已经派了个伙计名叫{.*}到{.*}附近接你，把镖车送到他那里就行了} {
        #system {echo "%%1 %%2 %%3">>data/hubiao.log};
        log hubiao 任务领取成功;
        
        #var hubiao[complete] 0;
        #var hubiao[pp][status] none;
        #var hubiao[luanru] 0;
        
        parse_addr %%1 hubiao[dest];
        #var hubiao[dest][name] %%3;
        #var hubiao[dest][huoji] %%2;
        
        log hubiao 目的地 ：$hubiao[dest][area] $hubiao[dest][name];
        
        find_path {$hubiao[dest][name]} {$hubiao[dest][area]};
        
        #if {"$map[gt][path]" == ""} {
            #show <139>找不到目的地路径 : $hubiao[dest][name] $hubiao[dest][area]<079>;
            log hubiao 找不到目的地路径 : $hubiao[dest][name] $hubiao[dest][area];
            
            #show <139>请手动指定目的地(hbpos)<079>;
            #nop hb_status_set manual;
        } {
            #show <129>找到路径 : $map[gt][path]<079>;
            
            #var hubiao[path_idx] 1;
            #list hubiao[path] create $map[gt][path];
            #list hubiao[path] size hubiao[path_len];
            
            hb_status_set zoubiao;
        
        };
    };
    #act {^认领任务失败，请选择其他任务} {
        hb_status_set listesc;
    };
    #act {^你上次运镖太辛苦了，下去休息休息吧} {
        log hubiao CD中，30秒后继续;
        
        #delay {dl_getesc} {
            hb_status_set listesc;
        }{30};
    };
    
    #class hb_status_getesc_inner close;
    
    #var hubiao[dest] {};
    
    #show <139>本次选中任务:$hubiao[quest][id] $hubiao[quest][boss]<079>;
    
    log hubiao 选中任务: $hubiao[quest][id] $hubiao[quest][boss];
    #nop #show 渡仁把这批红货送到嘉兴钱庄包方圆那里，他已经派了个伙计名叫时儿到嘉兴城附近接你，把镖车送到他那里就行了。;
    getesc $hubiao[quest][id];
    
};

#alias {hb_status_exit_findpath} {

};
#alias {hb_status_enter_findpath} {
    #class hb_status_findpath_inner open;
    
    
    
    #class hb_status_findpath_inner close;

    
};

#alias {hb_status_exit_manual} {

};
#alias {hb_status_enter_manual} {
    #class hb_status_manual_inner open;
    
    #act {^你累了个半死，终于把镖运到了地头} {
        #var hubiao[complete] 1;
        
        log hubiao 镖车交货完成;
        
        hb_status_set complete;
    };
    
    #class hb_status_manual_inner close;
    
    #show <139>请手动操作护镖<079>;
    #buffer lock;
};

#alias {hb_status_exit_zoubiao} {
    #undelay {dl_zoubiao_wait};
    #undelay {dl_pphuoji_wd};
    #undelay {dl_zoubiao_start};
    
    #class pphuoji_inner kill;
};
#alias {hb_status_enter_zoubiao} {
    #class hb_status_zoubiao_inner open;
    
    #alias {ganche} {
        #if {$hubiao[complete] == 0} {
            #if {$hubiao[path_idx] <= $hubiao[path_len]} {
                #if {"$hubiao[path][$hubiao[path_idx]]" == "u"} {
                    gup;
                };
                #elseif {"$hubiao[path][$hubiao[path_idx]]" == "d"} {
                    gdown;
                };
                #else {
                    g$hubiao[path][$hubiao[path_idx]];
                };
            } {
                pphuoji;
            };
        };
    };
    #alias {find_huoji} {
        find_path {$hubiao[dest][name1]} {$hubiao[dest][area1]};
        
        #if {"$map[gt][path]" == ""} {
            #show <139>找不到目的地路径 : $hubiao[dest][name1] $hubiao[dest][area1]<079>;
            log hubiao 找不到目的地路径 : $hubiao[dest][name1] $hubiao[dest][area1];
            
            #if {"$hubiao[pp][status]" == "CurrentRoom"} {
                #list hubiao[path] insert $hubiao[path_idx] $hubiao[roomexits][1];
                #list hubiao[path] size hubiao[path_len];
                
                ganche;
            } {
                
                #if {"$hubiao[pp][status]" == "trvs"} {
                    hb_status_set manual;
                } {
                    log hubiao 开始遍历，广度：2;
                    #var hubiao[pp][status] trvs;
                    bfs 2;
                    
                    #var hubiao[path] $map[trvs][path];
                    #list hubiao[path] size hubiao[path_len];
                    #var hubiao[path_idx] 1;
                    
                    ganche;
                };
                
            };
        } {
            #show <129>找到路径 : $map[gt][path]<079>;
            
            #list hubiao[path] create $map[gt][path];
            #list hubiao[path] size hubiao[path_len];
            #var hubiao[path_idx] 1;
            
            ganche;
        };
    };
    #alias {pphuoji} {
        #if {"$hubiao[pp][status]" == "none"} {
            #class pphuoji_inner open;
            
            #act {^ GPS : 目的地为当前房间} {
                #var hubiao[pp][status] CurrentRoom;
            };
            #act {告诉你：拼音：【%*】【%w %w】} {
                #unact {告诉你：拼音：【%*】【%w %w】};
                #var hubiao[dest][py] {%%%2 %%%3};
                
                #var hubiao[pp][status] pyFound;
                helpme find %%%2 %%%3;
            };
            #act {告诉你：【%*】目前在【%*的%*】,快去摁死它吧} {
                #var hubiao[pp][status] Found;
                
                #var hubiao[dest][area1] %%%2;
                #var hubiao[dest][name1] %%%3;
                
                #if {&map_area_table[$hubiao[dest][area1]] == 0} {
                
                } {
                    #var hubiao[dest][area1] $map_area_table[$hubiao[dest][area1]];
                };
                
                #class pphuoji_inner kill;
                
                find_huoji;
            };
            #delay {dl_pphuoji_wd} {
                log hubiao pphuoji超时，80秒没有反应;
                #var hubiao[pp][status] TimeOut;
                
                #class pphuoji_inner kill;
            }{80};
            
            #class pphuoji_inner close;
            
            #var hubiao[pp][status] py;
            helpme py $hubiao[dest][huoji];
        } {
            #class pphuoji_inner open;
            
            #act {^ GPS : 目的地为当前房间} {
                #var hubiao[pp][status] CurrentRoom;
            };
            
            #class pphuoji_inner close;
            
            on_map_pad {
                find_huoji;
            };
        };
    };
    
    #act {^你现在正忙着哩} {
        on_unbusy {
            ganche;
        };
    };
    #act {^%s这里%*{方向|出口}{是|有}%*} {
        #var hubiao[roomexits] @format_exits{%%5};
    };
    #act {^这个方向过不去} {
        log hubiao 路径出错;
        #nop #show <139>路径出错，请手动纠正<079>;
        
        #nop hb_status_set manual;
        
        on_map_pad {
            correct_path;
            #delay {dl_zoubiao_start} {
                ganche;
            }{2};
        };
    };
    #act {^你赶着镖车驶了过来} {
        #map move $hubiao[path][$hubiao[path_idx]];
        #math hubiao[path_idx] {$hubiao[path_idx] + 1};
        
        #delay {dl_zoubiao_wait} {
            on_unbusy {
                ganche;
            };
        }{2};
    };
    #act {^镖车还没有跟上来呢,走慢点} {
        #delay {dl_zoubiao_wait} {
            ganche;
        }{2};
    };
    #act {^{劫匪突然从暗处跳了出来|劫匪伸手一拦道|你还是先把对手解决了再说吧}} {
        hb_status_set robber;
    };
    #act {「店铺伙计」$hubiao[dest][huoji](%w %w} {
        log hubiao 发现收货的伙计 %%1 %%2;
        
        #undelay {dl_zoubiao_wait};
    };
    #act {^你累了个半死，终于把镖运到了地头} {
        #var hubiao[complete] 1;
        
        log hubiao 镖车交货完成;
        
        hb_status_set complete;
    };
    
    #alias {correct_path} {
        log hubiao 自动纠正路径;
        #show <139>自动纠正路径<079>;
        
        find_path {$hubiao[dest][name]} {$hubiao[dest][area]};
        
        #if {"$map[gt][path]" == ""} {
            #show <139>找不到目的地路径 : $hubiao[dest][area] $hubiao[dest][name]<079>;
            log hubiao 找不到目的地路径 : $hubiao[dest][area] $hubiao[dest][name];
            
            #nop hb_status_set manual;
            
            #list hubiao[path] insert $hubiao[path_idx] $hubiao[roomexits][1];
        } {
            #show <129>找到路径 : $map[gt][path]<079>;
            
            #var hubiao[path_idx] 1;
            #list hubiao[path] create $map[gt][path];
        };
        #list hubiao[path] size hubiao[path_len];
    };
    #class hb_status_zoubiao_inner close;
    
    #if {$hubiao[luanru] == 1} {
        on_map_pad {
            correct_path;
            #delay {dl_zoubiao_start} {
                #var hubiao[luanru] 0;
                ganche;
            }{2};
        };
    } {
        #delay {dl_zoubiao_start} {
            ganche;
        }{2};
    };
    
};

#alias {hb_status_exit_robber} {
    #undelay {dl_robber_check};
    #undelay {dl_change_status};
};
#alias {hb_status_enter_robber} {
    #class hb_status_robber_inner open;
    
    #alias {check_robber} {
        shou @lower{$status[id]} robber;
    };
    
    #act {^你想{要|}收劫匪为弟子} {
        log hubiao 发现劫匪,5秒后再检测;

        #delay {dl_robber_check} {
            check_robber;
        }{5};
    };
    #act {^你想收谁作弟子} {
        #undelay {dl_robber_check};
        log hubiao 没有检测到劫匪;
        
        #if {$hubiao[complete] == 1} {
            hb_status_set complete;
        } {
            hb_status_set zoubiao;
        };
    };
    #act {^{你|劫匪}{你战胜了劫匪|你哈哈大笑，说道|双手一拱，笑着说道|脸色一寒，说道|向后一纵，恨恨地说道|向后退了几步，说道}} {
        kill @lower{$status[id]} robber;
    };
    #act {^你战胜了劫匪} {
        kill robber;
    };
    #act {^劫匪叫道：点子扎手，扯呼} {
        #undelay {dl_robber_check};
        check_robber;
    };
    #act {^劫匪趁你不注意，推着镖车就跑，你赶紧追了上去} {
        log hubiao 镖车乱入;
        
        #var hubiao[luanru] 1;
    };
    #act {^你累了个半死，终于把镖运到了地头} {
        log hubiao 镖车交货完成;
        
        #var hubiao[complete] 1;
    };
    #act {^【谣言】某人: $status[name]护的镖被劫匪劫去了} {
        hb_status_set none;
    };
    
    #class hb_status_robber_inner close;
    
    yun powerup;
    
    #delay {dl_robber_check} {
        check_robber;
    }{2};
};
    
#alias {hb_status_exit_recover} {
    halt;
};
#alias {hb_status_enter_recover} {
    #class hb_status_recover_inner open;
    
    #act {^{劫匪突然从暗处跳了出来|劫匪伸手一拦道|你还是先把对手解决了再说吧}} {
        hb_status_set robber;
    };
    
    #class hb_status_recover_inner close;
};

#alias {hb_status_exit_luanru} {
    #var hubiao[luanru] 0;
};
#alias {hb_status_enter_luanru} {
    #class hb_status_luanru_inner open;
    
    #alias {luanru} {
        #list hubiao[path] insert $hubiao[path_idx] %%1;
        
        #map move @reverse{%%1};
        
        hb_status_set zoubiao;
    };
    
    #class hb_status_luanru_inner close;
    
    #show <139>镖车乱入,请手动处理<079>;
};

#alias {hb_status_exit_arrived} {
    
};
#alias {hb_status_enter_arrived} {
    #class hb_status_arrived_inner open;
    
    #class hb_status_arrived_inner close;
};

#alias {hb_status_exit_complete} {
    #undelay {dl_complete_back};
};
#alias {hb_status_enter_complete} {
    #class hb_status_complete_inner open;
    
    #act {^你获得了%*石炭【劣质】} {
        drop shi tan;
    };
    #act {^你向林震南打听有关『重置任务』的消息} {
        log hubiao 任务计数重置成功;
        
        #if {$hubiao[mixin][start] == 1} {
            hb_status_set mixin;
        } {
            hb_status_set none;
        };
    };
    #act {^林震南吩咐了旁边的镖头几句，转头对你道：「辛苦了} {
        #math hubiao[lunci] {$hubiao[lunci] + 1};
        
        #if {$hubiao[lunci] >= $hubiao[max_lunci]} {
            #var hubiao[lunci] 0;
            ask lin about 重置任务;
        } {
            #if {$hubiao[mixin][start] == 1} {
                hb_status_set mixin;
            } {
                hb_status_set none;
            };
        };
        
        #var shell_cmd {echo "#var hubiao {$hubiao}">data/hubiao_cache_$status[id]};
        #system $shell_cmd;
    };
    #act {^林震南递给你一封密信} {
        log hubiao 接到密信任务;
        
        #var hubiao[mixin][start] 1;
    };
    
    #class hb_status_complete_inner close;
    
    #delay {dl_complete_back} {
        on_map_pad {
            gt 林震南;
            
            on_there {
                ask lin about finish;
            };
        };
    } {2};
};

#alias {hb_status_exit_mixin} {
    #untic {tk_mixin_yao};
    #unact {http://pkuxkx.{net|com}/antirobot/robot.php?filename=%*};
};
#alias {hb_status_enter_mixin} {
    #class hb_status_mixin_inner open;
    
    #alias {kan_mixin} {
        #act {http://pkuxkx.{net|com}/antirobot/robot.php?filename=%*} {
            #unact {http://pkuxkx.{net|com}/antirobot/robot.php?filename=%*};
            fetch_captcha %%%2;
        };
        xian;
    };
    #alias {mxpos} {
        log hubiao 密信地点：%%1 %%2;
        
        #var hubiao[mixin][area] %%1;
        #var hubiao[mixin][room] %%2;
        
        #if {&map_area_table[$hubiao[mixin][area]] == 0} {
        
        } {
            #var hubiao[mixin][area] $map_area_table[$hubiao[mixin][area]];
        };
        
        gt $hubiao[mixin][room] $hubiao[mixin][area];
        
        on_there {
            zhao;
        };
    };
    
    #act {一个伙计挖着鼻屎走了出来，道：你找我啥事} {
        log hubiao 偷财物的伙计出现;
        
        look;
    };
    #act {^    卷走林震南财物的伙计 {.*}(%w)} {
        log hubiao 发现偷财物的伙计 %%1 %%2;
        
        #var hubiao[mixin][target][name] %%1;
        #var hubiao[mixin][target][id] %%2;
        
        #class hb_status_mixin_inner open;
        
        #act {^$hubiao[mixin][target][name]把一包财物砸向你，一转眼不见了} {
            log hubiao 得到财物;
        
            hb_status_set mixinok;
        };

        #tic {tk_mixin_yao} {
            ask @lower{$hubiao[mixin][target][id]} about yao;
        }{2};
        
        #class hb_status_mixin_inner close;
    };
    
    #class hb_status_mixin_inner close;
    
    kan_mixin;
};

#alias {hb_status_exit_mixinok} {
    #var hubiao[mixin][start] 0;
};
#alias {hb_status_enter_mixinok} {
    #class hb_status_mixinok_inner open;
    
    #act {^你给林震南一些失踪的财物} {
        log hubiao 财物已交还;
        
        hb_status_set none;
    };
    
    #class hb_status_mixinok_inner close;
    
    on_map_pad {
        gt 林震南;
        on_there {
            give cai wu to lin;
        };
    };
};


#class $mod_name close;

${mod_name}_load;
