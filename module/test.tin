#class $mod_name open;

#alias {${mod_name}_load} {
    #showme module $mod_name loaded;
    #unalias ${mod_name}_load;
};

#alias {${mod_name}_unload}{
    #showme module $mod_name unloaded.;
    #class $mod_name kill;
};

#ACTION {http://pkuxkx.net/antirobot/robot.php?filename=%*}
{
    #system {firefox http://pkuxkx.net/antirobot/robot.php?filename=%1};
};

#alias {_tstmsg} {
    #showme <139>test : %0<079>;
};
#alias {xue.start} {
    #var testfreq %3;
    
    #class xue.start.inner open;
    
    #tic {tk_xue}{xue %1 for %2 $testfreq}{1};
    #tic {tk_chihe} {eat liang;he jiudai}{60};
    #tic {tk_yunjing} {yun jing}{66};
    
    #act {^也许是缺乏实战经验，你%*总是无法领会} {
        xue.stop;
    };
    #act {^你今天太累了，结果什么也没有学到} {
        #untic {tk_xue};
        goto 休息室;
        on_there {
            #class xue.start.sleep open;
            
            #act {你刚在三分钟内睡过一觉} {
                _tstmsg 睡觉cd中,5秒后再尝试睡觉;
                #delay {dl_sleep} {
                    sleep;
                } {5};
            };
            #act {你一觉醒来，精神抖擞地活动了几下手脚} {
                #cr;
                goto 朱熹;
                on_there {
                    #tic {tk_xue} {xue zhu for literate 1} {1};
                };
                #class xue.start.sleep kill;
            };
            #class xue.start.sleep close;
            sleep;
        };
    };
    #nop 书生说道：您太客气了，这怎么敢当;
    #act {{看上去书生并不想教你|书生像是受宠若惊一样|书生说道：您太客气了}} {
        #untic {tk_xue};
        #show <139>test : 该给钱了<079>;
        give 1 gold to shu sheng;
        #delay {dl_restart_xue} {#tic {tk_xue}{xue shu sheng for literate $testfreq}{1};}{2};
    };
    #class xue.start.inner close;
    yun jing;
};
#alias {xue.stop} {
    #class xue.start.inner kill;
    #undelay {dl_restart_xue};
    #untic {tk_xue};
};
#alias {zq.start} {
    #class zq.start.inner open;
    
    #var murenid 1;
    
    #tic {tk_lwzq} {
        #if {$status[energy] < @eval{$status[energy_max] * 0.5}} {
            halt;
            zq.stop;
            #delay {2} {
                #act {你吐纳完毕，睁开双眼，站了起来} {
                    #unact {你吐纳完毕，睁开双眼，站了起来};
                    zq.start;
                };
                tuna 6000;
            };
        };
        #elseif {$status[hp_valid] < @eval{$status[hp_max] * 0.98}} {
            halt;
            zq.stop;
            #delay {2} {
                yun heal;
                zq.start;
            };
        };
        #else {
            #if {$status[hp] < @eval{$status[hp_valid] * 0.5}} {
                yun recover;
            };
            perform zui-quan.lingwu;
        };
    } {10};
    #tic {tk_chihe} {he jiudai;eat liang}{60};
    #act {^未有对手或者你和对方未处于战斗中，不能%*领悟醉拳} {
        fight mu ren;
    };
    #act {^你刚跟这个木人练过功} {
        
        #if {$murenid == 1} {
            #var murenid 2;
            fight mu ren 2;
        } {
            #var murenid 1;
            fight mu ren;
        };
    };
    #class zq.start.inner close;
    bei none;
    fight mu ren;
    perform zui-quan.lingwu;
};
#alias {zq.stop} {
    #class zq.start.inner kill;
};


#alias {chaojing} {
    #act {^http://pkuxkx.net/antirobot/robot.php?filename=%*} {
        #unact {^http://pkuxkx.net/antirobot/robot.php?filename=%*};
        #show %%0;
    } {4};
    #act {^云游僧人说道：记住你的工号：} {
        #unact {^云游僧人说道：记住你的工号：};
        goto 石经峪;
        on_there {
            #act {^你默写良久，终于抄完了，看样子可以回去领工钱了} {
                #unact {^你默写良久，终于抄完了，看样子可以回去领工钱了};
                goto 石门寺;
                on_there {
                    #act {^云游僧人说道：「报上你的口令！report 口令！」} {
                        #unact {^云游僧人说道：「报上你的口令！report 口令！」};
                        #showme <139>输入工号领工资(report 工号):<079>;
                        #buffer lock;
                    };
                    ask sengren about done;
                };
            };
            write jing;
        };
    };
    ask sengren about job;
};

#alias {on_test_pass} {
    #alias cb_on_test_pass_true #cr;
    #alias cb_on_test_pass_false #cr;
    
    #alias cb_on_test_pass_true {%1};
    #alias cb_on_test_pass_false {%2};
    
    #if {$tt == 1} {
        cb_on_test_pass_true;
    } {
        cb_on_test_pass_false;
    };
};
#alias {testa} {
    #var tt %1;
    on_test_pass {
        #show pass;
        #show test ok;
    } {
        #show fail;
        #show test failed;
    };
}

#alias {gozqt} {
    #class gozqt_inner open;
    
    #act {^%S%s-%s$} {
        #if {"%%1" == "紫气台"} {
            #class gozqt_inner kill;
        } {
            #show %%1;
        };
    };
    #tic {tk_gozqt} {ed;wu;westup} {2};
    
    #class gozqt_inner open;
}

#alias {showtime} {
    
    #class showtime_inner open;
    
    #act {^管事对你说道：你去%*表演一下吧} {
        #var showtime[pos] %%1;
    };
    #act {^管事吩咐手下给了你一套表演道具} {
        _tstmsg 领到道具，出发表演;
        goto $showtime[pos];
        on_there {
            biaoyan suizhuan;
        };
    };
    #act {^你只见好大的块砖头应声而碎，旁人看得张大口说不出话来} {
        _tstmsg 表演完毕;
        biaoyan suizhuan;
    };
    #act {^你已唱完戏了，回去报告吧} {
        goto 戏园子;
        on_there {
            ask guan about 完成;
        };
    };
    #act {^管事把唱戏道具收了回去} {
        _tstmsg 唱戏完成;
        #class showtime_inner kill;
    };
    
    #class showtime_inner close;
    
    ask guan about job;
};

#class $mod_name close;

${mod_name}_load;
