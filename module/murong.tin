#class $mod_name open;

#alias {${mod_name}_load} {
    #showme Module $mod_name loaded;
    #unalias ${mod_name}_load;
};

#alias {${mod_name}_unload}{
    #showme Module $mod_name unloaded;
    #class $mod_name kill;
};

#alias {${mod_name}_help}{
    #showme <139>模块帮助文档 : ${mod_name}<079>;
    
    /* TODO: place your HELP doc here */
    
    #showme  ;
    #showme <139> -> 按任意键继续...<079>;
    #buffer lock;
};

#alias {_mrmsg} {
    #showme <139>murong : %0<079>;
};

#var murong[start] 0;

#alias {mr} {
    goto 茶馆 苏州;
};
#alias {mrstart} {
    #var murong[start] 1;
    on_there {
        mrjob;
    };
    goto 茶馆 苏州;
};
#alias {mrfin} {
    #map get roomvnum murong[vnum];
    #if {$murong[vnum] == 433} {
        give xin to pu ren;
    } {
        on_there {
            give xin to pu ren;
        };
        goto 茶馆 苏州;
    };
};
#alias {mrfai} {
    #map get roomvnum murong[vnum];
    #if {$murong[vnum] == 433} {
        ask pu about fail;
    } {
        mr;
        on_there {
            mrfai;
        };
    };
};
#alias {mrstop} {
    #var murong[start] 0;
};
#alias {mrpos} {
    #var murong[room] %2;
    #var murong[area] %1;
    log $murong[area] $murong[room];
    mrgo;
    on_there {
        zhaojz;
    };
};
#alias {mrgo} {
    #if {"$murong[mode]" == "text"} {
        gotowork $murong[pos1];
    };
    #if {"$murong[mode]" == "image"} {
        gt $murong[room] $murong[area];
    };
};
#alias {mrjob} {
    ask pu ren about job;
};
#alias {zhaojz} {
    #class on_found_jiazei_inner open;
    
    #act {$status[name]发现的 慕容世家{家贼|内鬼}($status[id]'s murong jiazei)} {
        _mrmsg 发现家贼;
        yun powerup;
        #undelay {dl_search_jiazei};
        kill jiazei;
    };
    #act {^慕容世家{家贼|内鬼}%*，身穿布衣走了过来} {
        #undelay {dl_search_jiazei};
        kill jiazei;
    };
    #act {^这里{不能|禁止}战斗} {
        ask jiazei about fight;
    };
    #act {^慕容世家{家贼|内鬼}往%*离开} {
        #unact {^慕容世家{家贼|内鬼}往%*离开};
        #undelay {dl_search_jiazei};
        $direction[cn][%%2];
    };
    #act {^遍历路径行走完成} {
        #delay {dl_search_jiazei} { mrfai } {3};
    };
    #act {^这个方向没有出路} {
        #class on_found_jiazei_inner kill;
        on_map_pad {
            bfs 10;
        };
    };
    
    #class on_found_jiazei_inner close;
    
    #delay {dl_search_jiazei} {
        #class on_found_jiazei_inner open;
        #act {^%s这里%*{方向|出口}{是|有}%*} {
            #delay {dl_search_jiazei} { trvs.step } {1.5};
        };
        #class on_found_jiazei_inner close;
        bfs 10;
    } {2};
    
    look;
};

#act {^仆人叹道：家贼难防，有人偷走了少爷的信件，据传曾在以下地点附近出现，你去把它找回来吧} {
    #act {http://pkuxkx.net/antirobot/robot.php?filename=%*} {
        #unact {http://pkuxkx.net/antirobot/robot.php?filename=%*};
        #system {firefox http://pkuxkx.net/antirobot/robot.php?filename=%%1};
    };
    #var murong[mode] image;
};
#act {^仆人叹道：家贼难防，有人偷走了少爷的信件，据传曾在『%*』附近出现，你去把它找回来吧} {
    #var murong[mode] text;
    #var murong[pos] %1;
    #var murong[pos1] %1;
    #system {echo "%1">>data/murong.log};
    #if {"$murong[pos1]" == "神龙岛花园"} {
        mrfai;
    } {;
        mrgo;
        on_there {
            zhaojz;
        };
    };
    
};

#act {^慕容世家{家贼|内鬼}死了} {
    _mrmsg 家贼挂了，拿信;
    #class on_found_jiazei_inner kill;
    #delay {2} {gc};
};
#act {^你从慕容世家{家贼|内鬼}的尸体身上搜出一封信件} {
    on_there {
        give xin to pu ren;
    };
    on_map_pad {
        goto 茶馆 苏州;
    };
    
    _mrmsg 拿到信件，回城复命;
};
#act {^仆人忙着呢，等会吧} {
    #delay {1} {give xin to pu ren};
};
#act {^由于你成功的找回慕容复写给江湖豪杰的信件，被奖励} {
    #if {$murong[start] == 1} {
        #delay {mrstart} {mrjob} {3};
    };
};
#act {^仆人对着你摇了摇头说：「你刚做过任务，先去休息休息吧。」} {
    #delay {10} {mrjob};
};

#class $mod_name close;

${mod_name}_load;
