#class $mod_name open;

#alias {${mod_name}_load} {
    #showme Module $mod_name loaded;
    #unalias ${mod_name}_load;
};

#alias {${mod_name}_unload}{
    #showme Module $mod_name unloaded;
    #class $mod_name kill;
};

#alias {${mod_name}_help}{
    #showme <139>模块帮助文档 : ${mod_name}<079>;
    
    /* TODO: place your HELP doc here */
    
    #showme  ;
    #showme <139> -> 按任意键继续...<079>;
    #buffer lock;
};

#var murong[start] 0;
#var murong[trvs_level] 6;

#alias {mr} {
    goto 茶馆 苏州;
};
#alias {mrstart} {
    #undelay {dl_mrjob};
    #var murong[start] 1;
    #map get roomvnum murong[vnum];
    #if {$murong[vnum] == 433} {
        mrjob;
    } {
        on_there {
            mrjob;
        };
        goto 茶馆 苏州;
    };
};
#alias {askfin} {
    give xin to pu ren;
    #if {$murong[start] == 1}{
        log 10秒后继续任务;
        #delay {dl_mrjob}{mrjob}{10}:
    };
};
#alias {mrfin} {
    #map get roomvnum murong[vnum];
    #if {$murong[vnum] == 433} {
        askfin;
    } {
        on_there {
            askfin;
        };
        goto 茶馆 苏州;
    };
};
#alias {askfail} {
    log 放弃任务;
    
    #show <139>10秒后放弃任务<079>;
    
    #delay {dl_mrfai} {
        ask pu about fail;
    }{10};
    #if {$murong[start] == 1}{
        log 20秒后继续任务;
        #show <139>20秒后继续<079>;
        #delay {dl_mrjob}{mrjob}{20}:
    };
};
#alias {mrfai} {
    #class on_found_jiazei_inner kill;
    
    #map get roomvnum murong[vnum];
    #if {$murong[vnum] == 433} {
        askfail;
    } {
        mr;
        on_there {
            askfail;
        };
    };
};
#alias {mrstop} {
    #var murong[start] 0;
    #undelay {dl_mrjob};
    #undelay {dl_mrfai};
    on_there {};
};

#var murong[banned][area] {{神龙岛}{}{紫禁城}{}{灵鹫}{}{星宿}{}{杀手帮}{}{北京}{}{武当山}{}{丐帮}{}};
#var murong[banned][pos] {{平西王府秘室}{}{嘉兴山洞}{}{杭州提督府小道}{}{大轮寺积雪小路}{}{丝绸之路大沙漠}{}{泰山封禅台}{}{白驼山蛇谷树林}{}{归云庄桃花林}{}{归云庄迷魂阵}{}{襄阳黑松寨}{}{襄阳乱葬岗}{}{神龙岛花园}{}{福州密道}{}{福州密室}{}{白驼山蛇谷荒地}{}{杀手帮果林}{}{北京城墙}{}{华山悬崖}{}{泰山兵器库}{}{泰山茅屋}{}{扬州兵器库}{}};

#alias {mrpos} {
    #var murong[pos] %1;
    mrgo;
};

#alias {mrgo} {
    #if {"$murong[pos]" == ""} {
        log 未取得任务地点;
    } {
        #var murong[room] {};
        
        parse_addr $murong[pos] murong[room];
        
        #if {"$murong[room][area]" == "" && "$murong[room][name]" == ""} {
            log 解析地点失败;
            mrfai;
        } {
            log $murong[room][area] $murong[room][name];

            #if {&murong[banned][area][$murong[room][area]] == 0} {
                #if {&murong[banned][pos][$murong[pos]] == 0} {
                    mrgowork;
                } {
                    log 禁止的地点;
                    #show <139>禁止的地点<079>;
                    
                    mrfai;
                };
            } {
                log 禁止的区域;
                #show <139>禁止的地点<079>;
                    
                mrfai;
            };
        };
    };
};

#alias {mrgowork} {
    log 前往任务地点;
    
    #var murong[trvs_level] 6;
    
    #var murong[looked] 0;
    
    #act {$status[name]发现的 慕容世家{家贼|内鬼}($status[id]'s murong jiazei)} {
        #nop #unact {$status[name]发现的 慕容世家{家贼|内鬼}($status[id]'s murong jiazei)};
        
        log 提前发现家贼;
        kill jiazei;

        #var murong[looked] 1;
    };
    
    on_there {
        zhaojz;
    };
    
    #if {"$murong[room][area]" == "noarea"} {
        goto $murong[room][name];
    } {
        goto $murong[room][name] $murong[room][area];
    };
};

#alias {mrjob} {
    ask pu ren about job;
};
#alias {zhaojz} {
    #if {$murong[looked] == 1} {
        $var murong[trvs_level] 3;
    };
    #if {"%1" != "" } {
        #var murong[trvs_level] %1;
    };
    
    log 开始寻找家贼;
    
    #class on_found_jiazei_inner open;
    
    #act {$status[name]发现的 慕容世家{家贼|内鬼}($status[id]'s murong jiazei)} {
        log 发现家贼;
        #undelay {dl_search_jiazei};
        trvs.stop;
    
        follow jiazei;
        kill jiazei;
        
        #delay {1} {yun powerup};
    };
    #act {^你决定开始跟随慕容世家{家贼|内鬼}一起行动} {
        #unact {^慕容世家{家贼|内鬼}往%*离开};
    };
    #act {^慕容世家{家贼|内鬼}%*走了过来} {
        trvs.stop;
        follow jiazei;
        kill jiazei;
    };
    #act {^这里{不能|禁止|不准}战斗} {
        ask jiazei about fight;
    };
    #act {^慕容世家{家贼|内鬼}往%*离开} {
        #unact {^慕容世家{家贼|内鬼}往%*离开};
        #undelay {dl_search_jiazei};
        $direction[cn][%%2];
    };
    #act {^遍历路径行走完成} {
        #class on_found_jiazei_inner kill;
        #nop #delay {dl_search_jiazei} { mrfai } {3};
    };
    #act {^这个方向没有出路} {
        #class on_found_jiazei_inner kill;
        on_map_pad {
            on_there {
                zhaojz;
            };
            mrgo;
        };
    };
    
    #class on_found_jiazei_inner close;
    
    #if {"$murong[room][area]" == "长江%*" || "$murong[room][area]" == "黄河%*"} {
        #var murong[trvs_level] 30;
    }; 
    #delay {dl_search_jiazei} {
        bfs $murong[trvs_level];
    } {2};
    
    look;
};

#act {^仆人叹道：家贼难防，有人偷走了少爷的信件，据传曾在以下地点附近出现，你去把它找回来吧} {
    log 图片任务;
    #act {http://pkuxkx.net/antirobot/robot.php?filename=%*} {
        #unact {http://pkuxkx.net/antirobot/robot.php?filename=%*};
        #nop #system {firefox http://pkuxkx.net/antirobot/robot.php?filename=%%1};
        #nop #system {./script/getpic.sh $status[id] %%1 &};
        fetch_captcha %%1;
    };
    #var murong[mode] image;
};

#act {^仆人叹道：家贼难防，有人偷走了少爷的信件，据传曾在『%*』附近出现，你去把它找回来吧} {
    #var murong[mode] text;
    #var murong[pos] %1;
    
    #system {echo "%1">>data/murong.log};
    
    mrgo;
    
};

#act {^慕容世家{家贼|内鬼}死了} {
    log 家贼挂了，拿信;
    #class on_found_jiazei_inner kill;
    get silver from corpse;
    #delay {1} {get gold from corpse};
    #delay {2} {get xin from corpse};
};
#act {^你从慕容世家{家贼|内鬼}的尸体身上搜出一封信件} {
    log 拿到信件，回城复命;
    on_map_pad {
        mrfin;
    };
};
#act {^仆人忙着呢，等会吧} {
    #delay {1} {give xin to pu ren};
};
#act {^由于你成功的找回慕容复写给江湖豪杰的信件，被奖励} {
    log 慕容任务完成;
};
#act {^仆人对着你摇了摇头说：「你刚做过任务，先去休息休息吧。」} {
    log CD中,30秒后继续尝试;
    #delay {dl_mrjob} {mrjob}{30};
};
#act {^由于你没有找回慕容复丢失的信件，被扣除：} {

};
#act {^你获得了%*石炭【劣质】} {
    drop shi tan;
};

#class $mod_name close;

${mod_name}_load;
