#class $mod_name open;

#var result {};
#var status {};

#alias {${mod_name}_load}{
    #var status[action] 空闲;
    #var status[flag][bar] 1;
    #var status[flag][refresh_hp] 0;
    #var status[flag][fighting] 0;
    #format screen[rows] {%R};
    #format screen[cols] {%C};

    #split 2 2;
    set hpbrief long;
    #untic keep_alive;
    
    refresh_sc;
    #nop refresh_i;
    refresh_hp;
    statusme;
    hpbrief1;
    #nop #delay {init1}{refresh_hp}{1};
    #nop #delay {init2}{hpbrief1}{2};
    
    #if {$screen[cols] < 120}{
        #showme {screen width need 120 cols at least.};
        mod status unload;
    };
    #unalias ${mod_name}_load;
};

#alias {${mod_name}_unload}{
    #undelay init1;
    #undelay init2;
    
    #class sched open;
    #ticker {keep_alive} {#cr} {300};
    #class sched close;
    #split;
        
    #showme module $mod_name unloaded;
    #class $mod_name kill;
};

#list color_table[hp]		create 	{<ADA>}{<ADA>}{<BDA>}{<CDA>}{<DDA>}{<EDA>}{<FDA>}{<FCA>}{<FAA>}{<FAA>};
#list color_table[sp]		create 	{<AEC>}{<ADF>}{<BDF>}{<CDF>}{<DDF>}{<EDF>}{<FDF>}{<FCF>}{<FCF>}{<FBF>};
#list color_table[force]	create 	{<AAE>}{<ABE>}{<BBE>}{<CBE>}{<DBE>}{<EBE>}{<FBE>}{<FAE>}{<FAE>}{<FAC>};
#list color_table[energy]	create	{<BFF>}{<AEE>}{<BEE>}{<CEE>}{<DEE>}{<EEE>}{<FEE>}{<FDE>}{<FDE>}{<FCE>};
#list color_table[food]		create	{<AFC>}{<AEC>}{<BEC>}{<CEC>}{<DEC>}{<EEC>}{<FEC>}{<FDC>}{<FDC>}{<FCC>};
#list color_table[water]	create	{<AFC>}{<AEC>}{<BEC>}{<CEC>}{<DEC>}{<EEC>}{<FEC>}{<FDC>}{<FDC>}{<FCC>};
    
#function {fn_get_status_color}
{
	#var status_tmp @eval{%1*100/%2};

	#if {$status_tmp > 100}{#var color_code $color_table[%4][1]};
	#elseif {$status_tmp > 90}{#var color_code $color_table[%4][2]};
	#elseif {$status_tmp > 80}{#var color_code $color_table[%4][3]};
	#elseif {$status_tmp > 70}{#var color_code $color_table[%4][4]};
	#elseif {$status_tmp > 60}{#var color_code $color_table[%4][5]};
	#elseif {$status_tmp > 50}{#var color_code $color_table[%4][6]};
	#elseif {$status_tmp > 40}{#var color_code $color_table[%4][7]};
	#elseif {$status_tmp > 30}{#var color_code $color_table[%4][8]};
	#elseif {$status_tmp > 20}{#var color_code $color_table[%4][9]};
	#else {#var color_code $color_table[%4][10]};

	#if {"%3" == "f"}
	{
		#format color_code {%l}{$color_code};
	};
	
	#var result $color_code;

	#unvar color_code;
	#unvar status_tmp;
}

#function {fn_draw_status_bar}
{
	#var bar[size] 30;
	#var bar[code] @fn_get_status_color{%1;%2;b;%4};

	#var bar[width] @eval{%1*$bar[size]/%2};
	#if {$bar[width] == 0}
	{
		#if {%1 > 0}{#var bar[width] 1};
	};
	#var bar[valid_width] @eval{%3*$bar[size]/%2};

	#loop 0 @eval{$bar[size]-1} cnt
	{
		#if {$cnt == $bar[width]}{#var bar[code] {$bar[code]<EEE>}};
		#if {$cnt == $bar[valid_width]}{#var bar[code] {$bar[code]<CCC>}};
		#var bar[code] {$bar[code] };
	};
	#var bar[code] {$bar[code]<088>};

	#var result $bar[code];

	#unvar bar;
	#unvar cnt;
}

#alias {update_status_bar}
{
	#if {$status[flag][bar] == 0}{#return};

    #if {"%1" == "top"}
    {
        #nop #format status[bar_food] {%+4s%-30s %-5s/%+5s}
		    {食物}{@fn_draw_status_bar{$status[food];$status[food_max];$status[food_max];food}}{$status[food]}{$status[food_max]};

	    #nop #format status[bar_water] {%+4s%-30s %-5s/%+5s}
		    {饮水}{@fn_draw_status_bar{$status[water];$status[water_max];$status[water_max];water}}{$status[water]}{$status[water_max]};

	    #format status[str_food] {%+6s%c%-4s/%+4s [%+4s]%c}
		    {食物：}{@fn_get_status_color{$status[food];$status[food_max];b;food}}{$status[food]}{$status[food_max]}{$status[food_desc]}{<078>};

	    #format status[str_water] {%+6s%c%-4s/%+4s [%+4s]%c}
		    {饮水：}{@fn_get_status_color{$status[water];$status[water_max];b;water}}{$status[water]}{$status[water_max]}{$status[water_desc]}{<078>};
		    
		#format status[str_overall]	{%+6s%-16s}{状态：}{$status[overall]};
	    #format status[str_pot]		{%+6s%-8s}{潜能：}{$status[pot]};
	    #format status[str_exp]		{%+6s%-8s}{经验：}{$status[exp]};
	    #format status[str_level]	{%+6s%-7s}{等级：}{$status[level]};
	    #format status[str_id]		{%+4s%-6s}{ID：}{$status[id]};
	    #format status[str_name]	{%+6s%-8s}{姓名：}{$status[name]};
	    #format status[str_age]		{%+6s%-5s}{年龄：}{$status[age]岁};
	    #format status[str_ability]	{%+6s%-5s}{功力：}{$status[ability]年};
	    #format status[str_moral]	{%+6s%-8s}{道德：}{$status[moral]};
	    #format status[str_weight]	{%+6s%-4s}{负重：}{$status[weight_pct]};
	    #format status[str_guild]	{%+6s%-8s}{门派：}{$status[guild]};
	    #format status[str_loyal]	{%+6s%-5s}{门忠：}{$status[loyal]};
	    #format status[str_reputation] {%+6s%-6s}{声望：}{$status[reputation]};
	    #format status[str_money]	{%+6s%-8s}{存款：}{$status[money]};
	    
	    #echo {{<EEE><aaa>%-120s<078>}{-2}}
		    {$status[str_food]<EEE><aaa> $status[str_water]<EEE><aaa>  $status[str_pot] $status[str_exp] $status[str_level] $status[str_overall]};
	    #echo {{<DDD><aaa>%-120s<078>}{-1}}
		    {$status[str_id] $status[str_name] $status[str_age] $status[str_ability] $status[str_guild] $status[str_loyal] $status[str_moral] $status[str_reputation] $status[str_weight]};
    }
    {
        #math status[valid_hp_pct] ($status[hp_valid] * 100 / $status[hp_max]);
	    #math status[valid_sp_pct] ($status[sp_valid] * 100 / $status[sp_max]);

	    #format status[bar_hp] {%+4s%-30s %-5s/%+5s [%+3s\x25]}
		    {气血}{@fn_draw_status_bar{$status[hp];$status[hp_max];$status[hp_valid];hp}}{$status[hp]}{$status[hp_valid]}{$status[valid_hp_pct]};

	    #format status[bar_sp] {%+4s%-30s %-5s/%+5s [%+3s\x25]}
		    {精神}{@fn_draw_status_bar{$status[sp];$status[sp_max];$status[sp_valid];sp}}{$status[sp]}{$status[sp_valid]}{$status[valid_sp_pct]};

	    #format status[bar_force] {%+4s%-30s %-5s/%+5s}
		    {内力}{@fn_draw_status_bar{$status[force];$status[force_max];$status[force_max];force}}{$status[force]}{$status[force_max]};

	    #format status[bar_energy] {%+4s%-30s %-5s/%+5s}
		    {精力}{@fn_draw_status_bar{$status[energy];$status[energy_max];$status[energy_max];energy}}{$status[energy]}{$status[energy_max]};
		    
		#echo {{%-53s%-6s%-8s%-6s%+46s}{2}}{<078>$status[bar_sp]}{     |}{<129>当前活动<078>}{|     }{$status[bar_energy]};
	    #echo {{%-53s%-6s%-8s%-6s%+46s}{1}}{$status[bar_hp]}{     |}{<139>$status[action]<078>}{|     }{$status[bar_force]};
    }
}

#alias {refresh_hp} {
    #if {$status[flag][refresh_hp] == 0}
    {
        #var status[flag][refresh_hp] 1;
        
        #class hp1_inner open;
        
        
        #act {≡━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━≡}
        {
            #line {gag}
        };
        
        #act {≡━━━━━━━━━━━━━━━━━━━━━━━◎北大侠客行◎━━≡}
        {
            #line {gag};
            update_status_bar top;
	        #class hp1_inner kill;
        };
        
        #act {^【 精神 】%*} { #line gag};
        #act {^【 气血 】%*} { #line gag};
        
        #ACTION {^【 食物 】%s%%1%s/%s%%2%s[%%3]%*}
        {
	        #var status[food] %%1;
	        #var status[food_max] %%2;
	        #var status[food_desc] %%3;
	        #line {gag};
        }{5};

        #ACTION {^【 饮水 】%s%%1%s/%s%%2%s[%%3]%*}
        {
	        #var status[water] %%1;
	        #var status[water_max] %%2;
	        #var status[water_desc] %%3;
	        #line {gag};
        }{5};

        #ACTION {^【 状态 】%s%%1}
        {
	        #var status[overall] %%1;
	        #line {gag};
        }{5};
        
        #class hp1_inner close;
        
        #send hp;
        
        #delay {5}{#var status[flag][refresh_hp] 0}
    }
}

#alias {hnd_statusme}{
    #regexp {%1} {≡──◎个人状态◎───────────────────────────────────≡}
    {
        #var hnd_statusme[start] 1;
    };
    
    #regexp {%1} {　姓名：　%S%s　ＩＤ：　%S%s　级别：　%S%s　战斗力：　%d%s　}
    {
        #var status[name] &1;
        #var status[id] &3;
        #var status[ce] &7;
    };
                                  　
    #regexp {%1} {≡─────────────────────────────────◎ 北大侠客行 ◎──≡}
    {
        #line gag;
        #unvar hnd_statusme;
    };
    
    #if {$hnd_statusme[start] == 1} {#line gag};
};

#alias {statusme}{
    #class statusme_inner open;
    
    #act {≡──◎个人状态◎───────────────────────────────────≡}
    {
        #line gag;
    };
    #act {　姓名：　%S%s　ＩＤ：　%S%s　级别：　%S%s　战斗力：　%d%s　}
    {
        #line gag;
        #var status[name] %%1;
        #var status[id] %%3;
        #var status[ce] %%7;
        
    };
    #act {≡───────────────────────────────────────────≡}
    {
        #line gag;
        #class statusme_inner kill;
    };

    #class statusme_inner close;
    
    status_me -g;
};

#alias {hnd_sc} {
    #if {"%1" != ""}
    {
        #regexp {%1}{≡━◎个人档案◎━━━━━━━━━━━━━━━━━━━━━━━━≡}
        {
            #var hnd_sc[start] 1;
        };
        #regexp {$%1}{≡━━━━━━━━━━━━━━━━━━━━━━◎北大侠客行◎━━≡}
        {
            #line gag;
            
            #unvar hnd_sc;
            update_status_bar top;
        };
        #regexp {%1}{^【%s%S%s%S%s】%S{ |」}%S(%w)$}
        {
            #nop #var status[name] &8;
            #nop #var status[id] &9;
        };
        #regexp {%1}{^%s你是一位%S岁的%*婚%S性人类%*}
        {
            #var status[age] @ctd{&2};
            #var status[gender] &4;
        };
        #regexp {%1} {^%s银行存款：%s%S$}
        {
            #var status[money] &3;
        };
        #regexp {%1} {^%s实战经验：%s%S年功力%*}
        {
            #var status[ability] @ctd{&3};
        };
        #regexp {%1} {^%s等%s级：%s%S%s职%s业：%s%S%*}
        {
            #var status[level] &4;
            #var status[class] &8;
        };
        #regexp {%1} {^%s道%s德：%s%S%s潜%s能%*}
        {
            #var status[moral] &4;
        };
        #regexp {%1} {^%s师门忠诚：%s%S%s江湖声望：%s%S$}
        {
            #var status[loyal] &3;
            #var status[reputation] &6;
        };
        #regexp {%1} {你是%*第%*代弟子}
        {
            #var status[guild] &1;
        };
    };
    
    #if {$hnd_sc[start] == 1}{#line gag};
};

#alias {refresh_sc}
{
    #undelay refresh_sc_watchdog;
    event_handler_add {hnd_sc}{RECEIVED LINE};
	#send sc;
	#delay {refresh_sc_watchdog} {event_handler_del {hnd_sc}{RECEIVED LINE}} {3};
}

#alias {refresh_sc_old}
{
	#class refresh_sc_inner open;
	
    #var tmp_get_limit {};

    #event {RECEIVED LINE}
    {
        #var tmp_get_limit[rcvd_line] {%%1};
        #if {"$tmp_get_limit[rcvd_line]" != ""}
        {
            #regexp {$tmp_get_limit[rcvd_line]}{≡━◎个人档案◎━━━━━━━━━━━━━━━━━━━━━━━━≡}
            {
                #var tmp_get_limit[start] 1;
            };
            #regexp {$tmp_get_limit[rcvd_line]}{≡━━━━━━━━━━━━━━━━━━━━━━◎北大侠客行◎━━≡}
            {
                #line gag;
                update_status_bar top;
                #class refresh_sc_inner kill;
            };
            #regexp {$tmp_get_limit[rcvd_line]}{^【%s%S%s%S%s】%S{ |」}%S(%w)$}
            {
                #nop #var status[name] &8;
                #nop #var status[id] &9;
            };
            #regexp {$tmp_get_limit[rcvd_line]}{^%s你是一位%S岁的%*婚%S性人类%*}
            {
                #var status[age] @ctd{&2};
                #var status[gender] &4;
            };
            #regexp {$tmp_get_limit[rcvd_line]} {^%s银行存款：%s%S$}
            {
                #var status[money] &3;
            };
            #regexp {$tmp_get_limit[rcvd_line]} {^%s实战经验：%s%S年功力%*}
            {
                #var status[ability] @ctd{&3};
            };
            #regexp {$tmp_get_limit[rcvd_line]} {^%s等%s级：%s%S%s职%s业：%s%S%*}
            {
                #var status[level] &4;
                #var status[class] &8;
            };
            #regexp {$tmp_get_limit[rcvd_line]} {^%s道%s德：%s%S%s潜%s能%*}
            {
                #var status[moral] &4;
            };
            #regexp {$tmp_get_limit[rcvd_line]} {^%s师门忠诚：%s%S%s江湖声望：%s%S$}
            {
                #var status[loyal] &3;
                #var status[reputation] &6;
            };
            #regexp {$tmp_get_limit[rcvd_line]} {你是%*第%*代弟子}
            {
                #var status[guild] &1;
            };
        };
        
        #if {"$tmp_get_limit[start]" == "1"}{#line gag};
    };
    #class refresh_sc_inner close;

	#send sc;
}

#alias {refresh_i}
{
    #class refresh_i_inner open;
    
    #act {┌%*┐} {#line gag};
    #act {│%*│} {#line gag};
    #act {├%*┤} {#line gag};
    #act {t%*s}
    {
        #line {gag}; 
        update_status_bar top;
        #class refresh_i_inner kill
    };
    
    #act {^│%s你身上带着%*件东西%s(负重%s%S)%*}
    {
        #var status[weight_cnt] %%2;#var status[weight_pct] %%5;
        #line {gag}
    }{4};
    
    #class refresh_i_inner close;
    
    #send i
};

#alias {hnd_hb}{
    #if {"%1" != ""} {
        #if {$hnd_hb[line] == 3} {
            #regexp {%1}{^#%S,%S,%S,%S$}
            {
                #var status[zhenqi] &1;
                #var status[zhenyuan] &2;
                #var status[food] &3;
                #var status[water] &4;

                #unvar hnd_hb;
                
                #line gag;
                
                update_status_bar top;
                update_status_bar;
            };
        } {
            #regexp {%1}{^#%S,%S,%S,%S,%S,%S$}
            {
                #if {$hnd_hb[line] != 2} {
                    #var hnd_hb {};
                    #var hnd_hb[line] 2;

                    #var status[exp] &1;
	                #var status[pot] &2;
	                #var status[force_max] &3;
	                #var status[force] &4;
	                #var status[energy_max] &5;
	                #var status[energy] &6;
                } {
                    #var status[hp_max] &1;
	                #var status[hp_valid] &2;
	                #var status[hp] &3;
	                #var status[sp_max] &4;
	                #var status[sp_valid] &5;
	                #var status[sp] &6;
	                
	                #var hnd_hb[line] 3;
                };
                #line gag;
            };
        }; 
    };
};

#alias {hpbrief1}
{
    #undelay {hpbrief1_watchdog};
    event_handler_add {hnd_hb}{RECEIVED LINE};

	#send hpbrief;
	
	#delay {hpbrief1_watchdog} {event_handler_del {hnd_hb}{RECEIVED LINE}} {3};
};

#alias {hpbrief1_old}{
    #nop #经验，潜能，最大内力，当前内力，最大精力，当前精力;
    #nop #最大气血，有效气血，当前气血，最大精神，有效精神，当前精神;
    #nop set hpbrief long情况下可另显示一行 #真气，真元，食物，饮水;
    
    #class hpbf_inner open;
    
    #var hpbf_inner_buffer {};
    #var hpbf_inner_buffer[line] 1;
    #var hpbf_inner_buffer[start] 0;
    
    #event {RECEIVED LINE}
    {
        #var hpbf_inner_buffer[rcvd_line] {%%1};
        #if {"$hpbf_inner_buffer[rcvd_line]" != ""}
        {
            #if {$hpbf_inner_buffer[line]==3}
            {
                #regexp {$hpbf_inner_buffer[rcvd_line]}{^#%S,%S,%S,%S$}
                {
                    #var status[zhenqi] &1;
                    #var status[zhenyuan] &2;
                    #var status[food] &3;
                    #var status[water] &4;
                    
                    
                    update_status_bar top;
                    update_status_bar;
                    
                    #line gag;
                    #class hpbf_inner kill;
                };
            }
            {
                #regexp {$hpbf_inner_buffer[rcvd_line]}{^#%S,%S,%S,%S,%S,%S$}
                {
                    #if {$hpbf_inner_buffer[line]==1}
                    {
                        #var hpbf_inner_buffer[start] 1;
                        
                        #var status[exp] &1;
		                #var status[pot] &2;
		                #var status[force_max] &3;
		                #var status[force] &4;
		                #var status[energy_max] &5;
		                #var status[energy] &6;
                    }
                    {
                        #var status[hp_max] &1;
		                #var status[hp_valid] &2;
		                #var status[hp] &3;
		                #var status[sp_max] &4;
		                #var status[sp_valid] &5;
		                #var status[sp] &6;
                    };
                    #math hpbf_inner_buffer[line] $hpbf_inner_buffer[line] + 1;
                };
            }; 
        };
        #if {$hpbf_inner_buffer[start] == 1 } {#line gag};
    };
    
    #class hpbf_inner close;
    
    hpbrief
};

#alias {status.set_action}
{
	#var status[action] {%1};
	update_status_bar
};

#alias {status.bar}
{
	#if {$status[flag][bar] == 1}
	{
		#var status[flag][bar] 0;
		#split;
	}{
		#var status[flag][bar] 1;
		#split 2 2;
		hpbrief1;
	};
};

#event {SCREEN RESIZE}
{
    #var screen[rows] %1;
    #var screen[cols] %0;
    
    #if {$screen[cols] < 120}{
        #showme {screen width need 120 cols at least.};
        mod status unload;
    };
    update_status_bar top;
    update_status_bar;
};

#TICKER {status_refresh}{hpbrief1}{15};
#TICKER {status_refresh2}{refresh_hp}{600};
#TICKER {status_refresh3}{refresh_sc;statusme}{1500};

#act {你在攻击中不断积蓄攻势。(气势：%1%%)}{#var status[mo] %1;#var status[flag][fighting] 1;status.set_action 气势$status[mo]%};
#act {你逐步从之前战斗的紧张气氛中平缓下来。(气势：%1%%)}{#var status[mo] %1;status.set_action 气势$status[mo]%};
#act {经过一段时间后，你终于完全从紧张地战斗氛围中解脱出来}{#var status[mo] 0;#var status[flag][fighting] 0;status.set_action 气势$status[mo]%};
#act {你%*大喝一声：看招}{#var status[flag][fighting] 1};
#act {你慢慢地移动著脚步，伺机出手}{#var status[flag][fighting] 1};
#act {你战胜了}{#var status[flag][fighting] 0};

#act {^#SESSION%*ACTIVATED%*}{hpbrief1};
#act {%*你%*请教%*疑问%*}{hpbrief1};
#act {%*你%*练习%*}{hpbrief1};
#act {^你突然感到精神一振，浑身似乎又充满了力量！} {hpbrief1};
#act {^( 你似乎有些疲惫，但是仍然十分有活力。 )} {hpbrief1};
#act {^( 你受了几处伤，不过似乎并不碍事。 )} {hpbrief1};
#act {^( 你动作似乎开始有点不太灵光，但是仍然有条不紊。 )} {hpbrief1};
#act {^( 你%*，看起来状况并不太好。 )} {hpbrief1};
#act {^( 你已经%*，正在勉力支撑著不倒下去。 )} {hpbrief1};
#act {^( 你看起来已经力不从心了。 )} {hpbrief1};
#act {^( 你气息粗重，动作开始散乱，看来所受的伤著实不轻。 )} {hpbrief1};
#act {^( 你似乎十分疲惫，看来需要好好休息了。 )} {hpbrief1};
#act {^( 你受了相当重的伤，只怕会有生命危险。 )} {hpbrief1};
#act {^( 你%*，眼看就要倒在地上。 )} {hpbrief1};
#act {^( 你已经陷入半昏迷状态，随时都可能摔倒晕去。 )} {hpbrief1};
#act {^( 你受伤过重，已经有如风中残烛，随时都可能断气。 )} {hpbrief1};
#act {^你深深吸了几口气，脸色看起来好多了。} {hpbrief1};
#act {^你领悟出一些基本轻功方面的窍门。} {hpbrief1};
#act {^你身子发虚}{hpbrief1};
#act {你拿起干粮咬了几口}{hpbrief1};
#act {你%*喝了%*}{hpbrief1};
#act {你全身放松，坐下来开始运功疗伤}{hpbrief1};
#act {你吃下一包金创药} {hpbrief1};

#act {你五心向天，排除一切杂念，内息顺经脉缓缓流动} {
    #nop #tic {tk_yunqi}{hpbrief1}{1};
};
#act {你将内息走了个小周天，流回丹田，收功站了起来} {
    #nop #untic tk_yunqi;
    hpbrief1;
};
#act {你坐下来运气}{
    #tic {tk_yunqi}{hpbrief1}{1};
};
#act {你运功完毕}{
    #untic tk_yunqi;
};

#class $mod_name close;

${mod_name}_load;
